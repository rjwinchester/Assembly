/*
** 
** HLAcmp-
**
** This program compares the output of two HLA compilations.
** It returns exit status zero if the two files are identical.  
** It returns a non-zero exit code if the files are not equal.
*/

program HLAcmp;

#include( "stdlib.hhf" );

const
	fsize1	:text := "(type dword filesize1)";
	fsize2	:text := "(type dword filesize2)";

static
	file1		:dword;
	filename1	:string;
	filesize1	:qword;
	buffer1		:dword;
	
	file2		:dword;
	filename2	:string;
	filesize2	:qword;
	buffer2		:dword;			
	
	DelFromHere:dword;
	
	ReturnVal:	uns32 := 0;	
	
#if( !@defined( filesys ))

	namespace filesys;

		procedure size( filename:string ); @external;

	end filesys;
	

	procedure filesys.size( filename:string );
	begin size;
	
		fileio.size( filename );
		xor( edx, edx );
		
	end size;
	
#endif



begin HLAcmp;

	// Verify command line syntax:
	
	
	if( arg.c() <> 3 ) then

		stdout.put( "Usage: hlacmp hlaOutFile1 hlaOutFile2" nl );
		arg.c();
		stdout.put( "Arguments: ", (type uns32 eax ), nl );
		arg.cmdLn();
		stdout.put( "Current command line: '", (type string eax ), "'", nl );
		os.exitProcess( 1 );

	endif;
	
	// Get the filenames:
	
	arg.v(1);
	mov( eax, filename1 );
	arg.v(2);
	mov( eax, filename2 );
	stdout.put( "Comparing ", filename1, " and ", filename2, "...", nl );
	
	// Get the file sizes:
	
	filesys.size( filename1 );
	mov( edx:eax, filesize1 );
	
	filesys.size( filename2 );
	mov( edx:eax, filesize2 );
	
	if( eax <> fsize1 && edx <> (type dword filesize1[4])) then
	
		stdout.put
		( 
			"File sizes do not agree:" nl
			"size( ", filename1, " ) = ", (type uns64 filesize1), nl,
			"size( ", filename2, " ) = ", (type uns64 filesize2), nl
		);
		os.exitProcess(3);
		
	endif;
			
	// Allocate storage to hold the two files
	// (note: for testing, file sizes never exceed 2GB):
	
	assert( (type dword filesize1[4]) = 0 );
	assert( (type dword filesize2[4]) = 0 );
	
	mem.alloc( fsize1 );
	mov( eax, buffer1 );

	mem.alloc( fsize2 );
	mov( eax, buffer2 );
	

	// Open the files:
	
	fileio.open( filename1, fileio.r );
	mov( eax, file1 );
	fileio.read( eax, val buffer1, fsize1 );
	fileio.close( file1 );
	
	fileio.open( filename2, fileio.r );
	mov( eax, file2 );
	fileio.read( eax, val buffer2, fsize2 );
	fileio.close( file2 );

	cld();
	mov( buffer1, esi );
	mov( buffer2, edi );
	mov( fsize1, ecx );
	repe.cmpsb();
	
	if( @e ) then
	
		stdout.put( "Files compare OK" nl nl );
		os.exitProcess(0);
		
	endif;
	neg( ecx );
	add( fsize1, ecx );
	stdout.put( "Files do not match! Offset=", (type int32 ecx), nl nl );
	os.exitProcess( 1 );	

end HLAcmp;
