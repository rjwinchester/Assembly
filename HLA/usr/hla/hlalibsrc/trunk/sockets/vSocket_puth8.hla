
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit socketUnit;
		

#includeOnce( "sockets.hhf" )
#includeOnce( "conv.hhf" )
#includeOnce( "excepts.hhf" )


/*******************************************************/
/*                                                     */
/* baseSocketClass_t.puth8-                            */
/*                                                     */
/* Outputs a byte to a socket as a hexadecimal string. */
/*                                                     */
/*******************************************************/


method vBaseSocketClass_t.puth8( h:byte ); @noframe; @nodisplay;
var
	eaxSave		:dword;
	ediSave		:dword;
	esiSave		:pointer to vBaseSocketClass_t;


	procedure vputh8Thunk( thunksEBPval:dword ); @noframe; @nodisplay;
	begin vputh8Thunk;
	
		push( ebp );			// Thunks always do this.
		mov( [esp+8], ebp );	// Retrieve EBP value passed as parameter
		
		mov( ebp::esiSave, esi );
		(type vBaseSocketClass_t [esi]).puts( (type string eax));
		
		pop( ebp );
		ret( _parms_ );
		
	end vputh8Thunk;
	
begin puth8;

	push( ebp );
	mov( esp, ebp );
	sub( _vars_, esp );
	mov( esi, esiSave );
	mov( edi, ediSave );
	mov( eax, eaxSave );
	
	hla.puth8
	( 
		#{	// Push thunk object on stack: 
			pushd( &vputh8Thunk );
			push( ebp );
		}#, 
		h 
	);

	mov( eaxSave, eax );
	mov( ediSave, edi );
	mov( esiSave, esi );
	leave();
	ret( _parms_ );
		
end puth8;
	
end socketUnit;

