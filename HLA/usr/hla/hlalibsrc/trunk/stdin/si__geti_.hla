
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit StdInput;
#includeOnce( "../include/stdinunit.hhf" )
#includeOnce( "../include/conversions.hhf" )


/*************************************************************/
/*                                                           */
/* _geti_                                                    */
/*                                                           */
/* Reads a 64-bit signed integer from the standard input and */
/* returns the numeric equivalent in EDX:EAX.                */
/*                                                           */
/* This routine will generate an appropriate exception       */
/* if there is a conversion error, overflow, or other        */
/* problem with reading the integer from the standard        */
/* input device.                                             */
/*                                                           */
/* Note that this routine will skip over any leading         */
/* delimiter characters (see the Delimiters cset in          */
/* stdlibdata.hla).  Furthermore, the value must end with    */
/* a valid delimiter character or an exception occurs.       */
/*                                                           */
/*************************************************************/

procedure _geti_; 
	@noframe;
	@nodisplay;
	
begin _geti_;

	push( ebx );
	push( ecx );
	push( esi );

	// If the input buffer is empty, read a new line from the
	// standard input device.
	
	cmp( NeedsInput, false );
	je gotInput;
	
		ReadLn();
		
	gotInput:

	mov( _hla.get_Delimiters(), edx );
	xor( eax, eax );
	mov( InputIndex, ebx );
	mov( CharsInBuf, ecx );
	SkipDelimsLoop:
	
		mov( InputBuffer[ ebx ], al );
		cmp( al, '_' );
		je isDelim;
		
			bt( eax, (type dword [edx] ));	// Delimiters
			jnc NoMoreDelims;
			
		isDelim:
		inc( ebx );
		cmp( ebx, ecx );
		jb SkipDelimsLoop;
		
		// We are at the end of the line, so read a new line from the user.
		
		ReadLn();
		mov( InputIndex, ebx );
		mov( CharsInBuf, ecx );
		jmp SkipDelimsLoop;
		
	NoMoreDelims:

	// Point esi at the start of the text and call _atoi to
	// convert this text to an integer.
	
	lea( esi, InputBuffer[ebx] );
	
	try
		
		conv.atoi64( [esi] );		// Convert to an integer.

		// Verify that we end with a valid delimiter:

		push( eax );
		mov( _hla.get_Delimiters(), ebx );
		pop( eax );
		movzx( (type char [esi]), ecx );
		bt( ecx, [ebx] );		// Delimiters
		jc goodDelim;

			raise( ex.ConversionError );

		goodDelim:
		
	  anyexception
	
		mov( true, NeedsInput);
		reraise( eax );
				
	endtry;
	
	// Compute new InputIndex value.
	
	sub( &InputBuffer, esi );
	mov( esi, InputIndex );

	pop( esi );
	pop( ecx );
	pop( ebx );
	ret();
	
end _geti_;


end StdInput;




	

