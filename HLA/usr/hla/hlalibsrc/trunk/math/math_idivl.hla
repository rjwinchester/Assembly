
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit mathUnit;

#include( "math.hhf" )

/*********************************************************/
/*                                                       */
/* idivl( left, right, dest -                            */
/*                                                       */
/* Computes dest := left idiv right where all the        */
/* operands are 128-bit operands.  Does a quick check    */
/* to see if it can do a 128/32 division vs. a full      */
/* 128/128 bit division.                                 */
/*                                                       */
/*********************************************************/


procedure math.idivl( left:lword; right:lword; var dest:lword );
	@nodisplay;
	@noalignstack;
	@noframe;
	
var
	Remainder	:lword;
	sign		:boolean;
	
begin idivl;

	push( ebp );
	mov( esp, ebp );
	sub( _vars_, esp );
	
	push( eax );
	push( ecx );
	mov( dest, ecx );
	
	// Determine the sign of the result:
	
	mov( false, sign );
	cmp( (type byte left[15]), 0 );
	jnl itsNotNeg;
	
		mov( true, sign );
		xor( eax, eax );
		sub( (type dword left[0]), eax );
		mov( eax, (type dword left[0]) );
		
		mov( 0, eax );
		sbb( (type dword left[4]), eax );
		mov( eax, (type dword left[4]) );
		
		mov( 0, eax );
		sbb( (type dword left[8]), eax );
		mov( eax, (type dword left[8]) );
		
		mov( 0, eax );
		sbb( (type dword left[12]), eax );
		mov( eax, (type dword left[12]) );
		
	itsNotNeg:
	
	cmp( (type byte right[15]), 0 );
	jnl itsNotNeg2;
	
		xor( true, sign );
		xor( eax, eax );
		sub( (type dword right[0]), eax );
		mov( eax, (type dword right[0]) );
		
		mov( 0, eax );
		sbb( (type dword right[4]), eax );
		mov( eax, (type dword right[4]) );
		
		mov( 0, eax );
		sbb( (type dword right[8]), eax );
		mov( eax, (type dword right[8]) );
		
		mov( 0, eax );
		sbb( (type dword right[12]), eax );
		mov( eax, (type dword right[12]) );
		
	itsNotNeg2:
	
	// Next, check to see if the H.O. dword of the divisor (right)
	// is zero.  If so, we can do a 64/32 bit division using the
	// div instruction (which is faster).
	
	mov( (type dword right[12]), eax );
	or( (type dword right[8]), eax );
	or( (type dword right[4]), eax );
	jnz fullDivision;
	
		
		// Okay, we can safely do a 128/32 bit division here.
		// Note that if the divisor is zero, the system will
		// raise the ex.DivideError exception, so we don't
		// have to check here.
		
		push( ebx );
		push( edx );
		
		mov( (type dword right), ebx );
		mov( (type dword left[4]), edx );
		or( (type dword left[8]), edx );
		or( (type dword left[12]), edx );
		jnz notDiv32;
		
			// We can do a 32/32 bit division!
					
			mov( (type dword left), eax );
			div( ebx );
			
			mov( eax, [ecx] );
			mov( 0, (type dword [ecx+4]));
			mov( 0, (type dword [ecx+8]));
			mov( 0, (type dword [ecx+12]));
			jmp didDiv32;
			
		notDiv32:
		
			// We have to do a full 128/32 bit division
			
			mov( (type dword left[12]), eax );
			xor( edx, edx );
			div( ebx );
			mov( eax, [ecx+12] );
			
			mov( (type dword left[8]), eax );
			div( ebx );
			mov( eax, [ecx+8] );
			
			mov( (type dword left[4]), eax );
			div( ebx );
			mov( eax, [ecx+4] );
			
			mov( (type dword left), eax );
			div( ebx );
			mov( eax, [ecx] );
			
		didDiv32:				   
		pop( edx );
		pop( ebx );
		jmp DivDone;
			
	fullDivision:
	
		// Must do a full 128/128 division.
		// Note: divisor is not zero at this point.
		
		math.div128( left, right, [ecx], Remainder );
	
	DivDone:
	
	// If the result is supposed to be negative, negate the result:
	
	cmp( sign, 0 );
	jz noNegation;
	
		xor( eax, eax );
		sub( [ecx], eax );
		mov( eax, [ecx] );
		
		mov( 0, eax );
		sbb( [ecx+4], eax );
		mov( eax, [ecx+4] );
		
		mov( 0, eax );
		sbb( [ecx+8], eax );
		mov( eax, [ecx+8] );
		
		mov( 0, eax );
		sbb( [ecx+12], eax );
		mov( eax, [ecx+12] );
		
	noNegation:
	pop( ecx );
	pop( eax );
	leave();
	ret( _parms_ );
	
end idivl;

end mathUnit;
