unit wListBox_u;
?@NoDisplay := true;
?@NoStackAlign := true;

#include( "howl.hhf" )
#include( "excepts.hhf" )
#include( "strings.hhf" )
#include( "memory.hhf" )

#includeOnce( "../include/howlunit.hhf" )		
#includeOnce( "stdio.hhf" )
#includeOnce( "stdout.hhf" )


readonly		
	align( 4 );
	vmt( wListBox_t );

/////////////////////////////////////////////////////////////////////////////////
//
// List Box objects.
//


// Constructor for the wListBox_t class:

procedure wListBox_t.create_wListBox
(
	wlbName		:string;
	parent		:dword; 									
	x			:dword; 
	y			:dword;
	width		:dword;
	height		:dword;
	sort		:boolean;
	onClick		:widgetProc 
);;
	
_begin( wListBox_t, create_wListBox );
	
	push( eax );
	push( ecx );
	push( edx );
	
	if( esi = NULL ) then
	
		mem.alloc( @size( wListBox_t ));
		mov( eax, esi );
		mov( true, cl );
		
	else
	
		mov( this.wBase_private.onHeap, cl );
		
	endif;

	// We need to create a window that will hold this object.

	(type wClickable_t [esi]).create_wClickable
	(
		wlbName,
		parent,									
		x,		
		y,		
		width,	
		height,	
		onClick	  
	);	
	mov( cl, this.wBase_private.onHeap );
	mov
	(
		w.WS_CHILD | w.WS_VISIBLE | w.WS_TABSTOP | w.WS_GROUP |
			w.LBS_NOTIFY | w.LBS_STANDARD,
		eax
	);
	if( sort ) then
	
		or( w.LBS_SORT, eax );
		
	endif;
	w.CreateWindowEx
	(
		0,
		"LISTBOX",							// Button class name
		"list box",							// ListBox caption
		eax,								// ListBox style
		x,
		y,
		width,
		height,
		parent,
		this.wBase_private.objectID,
		howl.howlInstance,
		NULL
	);
	mov( eax, this.handle );
	w.SetWindowLong( eax, w.GWL_USERDATA, esi );
	
	// Initialize the type string field:
		
	howl.setType( wListBox_c );
	
	// Initialize the VMT pointer:
	
	mov( &wListBox_t._VMT_, this._pVMT_ );
	
	pop( edx );
	pop( ecx );
	pop( eax );
	
_end( wListBox_t, create_wListBox );


// Accessor functions:

method wListBox_t.get_textColor; @noframe;;
_begin( wListBox_t, get_textColor );

	mov( this.wListBox_private.textColor, eax );
	ret();
	 
_end( wListBox_t, get_textColor );





// Mutator functions:


method wListBox_t.set_textColor( textColor:dword );;
_begin( wListBox_t, set_textColor );

	push( eax );
	push( ecx );
	push( edx );
	
	mov( textColor, eax );
	mov( eax, this.wListBox_private.textColor );
	w.InvalidateRect( this.handle, NULL, true );
	
	pop( edx );
	pop( ecx );
	pop( eax );
	
_end( wListBox_t,set_textColor );










// add_string-
//
//	If "sort" was false, appends a string to the end of a list box;
// if "sort" was true, inserts the string at the appropriate point
// in the list box.
 
method wListBox_t.add_string( s:string );;						
_begin( wListBox_t, add_string );

	push( eax );
	push( ecx );
	push( edx );
	
	w.SendMessage( this.handle, w.LB_ADDSTRING, 0, s );
	if( eax = w.LB_ERRSPACE ) then
	
		raise( ex.MemoryAllocationFailure );
		
	endif;
	
	pop( edx );
	pop( ecx );
	pop( edx );

_end( wListBox_t, add_string );


// Insert a string into a list box at the specified index.

method wListBox_t.insert_string( index:dword; s:string );;						
_begin( wListBox_t, insert_string );

	push( eax );
	push( ecx );
	push( edx );
	
	w.SendMessage( this.handle, w.LB_INSERTSTRING, index, s );
	if( eax = w.LB_ERRSPACE ) then
	
		raise( ex.MemoryAllocationFailure );
		
	elseif( eax = w.LB_ERR ) then
	
		raise (ex.ValueOutOfRange );
		
	endif;
	
	pop( edx );
	pop( ecx );
	pop( edx );

_end( wListBox_t, insert_string );



// delete_string-
//	Deletes the string at index "index" from the list box:

method wListBox_t.delete_string( index:dword );;				
_begin( wListBox_t, delete_string );

	push( eax );
	push( ecx );
	push( edx );
	
	w.SendMessage( this.handle, w.LB_DELETESTRING, index, 0 );
	if( eax = w.LB_ERRSPACE ) then
	
		raise( ex.ValueOutOfRange );
		
	endif;
	
	pop( edx );
	pop( ecx );
	pop( edx );

_end( wListBox_t, delete_string );


// reset-
//	Clears all the text from the list box

method wListBox_t.reset;;										
_begin( wListBox_t, reset );

	push( eax );
	push( ecx );
	push( edx );
	
	w.SendMessage( this.handle, w.LB_RESETCONTENT, 0, 0 );
	
	pop( edx );
	pop( ecx );
	pop( edx );

_end( wListBox_t, reset );


// find_prefix-
//
//	Finds the first entry in the list box that begins with the specified
// prefix string. Returns the index into the list box where the entry was
// found or w.LB_ERR if no such string exists.

method wListBox_t.find_prefix( s:string; startIndex:dword );;		
_begin( wListBox_t, find_prefix );

	push( ecx );
	push( edx );
	
	w.SendMessage( this.handle, w.LB_FINDSTRING, startIndex, s );
	
	pop( edx );
	pop( ecx );

_end( wListBox_t, find_prefix );


// find_strings-
//
//	Finds the first entry in the list box that matches the specified
// string. Returns the index into the list box where the entry was
// found or w.LB_ERR if no such string exists.

method wListBox_t.find_string( s:string; startIndex:dword );;		
_begin( wListBox_t, find_string );

	push( ecx );
	push( edx );
	
	w.SendMessage( this.handle, w.LB_FINDSTRINGEXACT, startIndex, s );
	
	pop( edx );
	pop( ecx );

_end( wListBox_t, find_string );



// get_count-
//
//	Returns the number of items in the list box.

method wListBox_t.get_count;;					
_begin( wListBox_t, get_count );

	push( ecx );
	push( edx );
	
	w.SendMessage( this.handle, w.LB_GETCOUNT, 0, 0 );
	
	pop( edx );
	pop( ecx );

_end( wListBox_t, get_count );


// get_curSel
//
//	Returns the index of the currently selected item in the list box.
// Will return w.LB_ERR if no item is selected.

method wListBox_t.get_curSel;;					
_begin( wListBox_t, get_curSel );

	push( ecx );
	push( edx );
	
	w.SendMessage( this.handle, w.LB_GETCURSEL, 0, 0 );
	
	pop( edx );
	pop( ecx );

_end( wListBox_t, get_curSel );


// get_itemData-
//	Returns the 32-bit data value associated with the 
// object specified by the index (set by set_itemData)

method wListBox_t.get_itemData( i:dword );;		
_begin( wListBox_t, get_itemData );

	push( ecx );
	push( edx );
	
	w.SendMessage( this.handle, w.LB_GETCURSEL, i, 0 );
	
	pop( edx );
	pop( ecx );

_end( wListBox_t, get_itemData );


// a_get_text-
//	Retrieves the string associated with the list item at
// index "i".  Storage for the string is allocated on the heap.

method wListBox_t.a_get_text( i:dword );;		
_begin( wListBox_t, a_get_text );

	push( ecx );
	push( edx );
	w.SendMessage( this.handle, w.LB_GETTEXTLEN, i, 0 );
	if( eax = w.LB_ERR ) then
	
		raise( ex.ValueOutOfRange );
		
	endif;
	str.alloc( eax );
	push( eax );
	w.SendMessage( this.handle, w.LB_GETTEXT, i, eax );
	pop( ecx );
	if( eax = w.LB_ERR ) then
	
		raise( ex.ValueOutOfRange );
		
	endif;
	mov( eax, (type str.strRec [ecx]).length );
	mov( 0, (type byte [ecx+eax]) );
	mov( ecx, eax );
	
	pop( edx );
	pop( ecx );

_end( wListBox_t, a_get_text );


// get_text-
//
//	Retrieves a string from the list box at the specified
// index and stores the character data into the 's' parameter.

method wListBox_t.get_text( i:dword; s:string );;				
_begin( wListBox_t, get_text );

	push( eax );
	push( ecx );
	push( edx );
	
	w.SendMessage( this.handle, w.LB_GETTEXTLEN, i, 0 );
	if( eax = w.LB_ERR ) then
	
		raise( ex.ValueOutOfRange );
		
	endif;
	mov( s, ecx );
	if( eax > (type str.strRec [ecx]).length ) then
	
		raise( ex.StringOverflow );
		
	endif;
	w.SendMessage( this.handle, w.LB_GETTEXT, i, s );
	mov( s, ecx );
	mov( eax, (type str.strRec [ecx]).length );
	
	pop( edx );
	pop( ecx );
	pop( eax );

_end( wListBox_t, get_text );



method wListBox_t.set_curSel(index:dword);;	
_begin( wListBox_t, set_curSel );

	push( eax );
	push( ecx );
	push( edx );
	
	w.SendMessage( this.handle, w.LB_SETCURSEL, index, 0 );
	
	pop( edx );
	pop( ecx );
	pop( eax );

_end( wListBox_t, set_curSel );


method wListBox_t.set_itemData
(
	index	:dword;
	data	:dword
);;
_begin( wListBox_t, set_itemData );

	push( eax );
	push( ecx );
	push( edx );
	
	w.SendMessage( this.handle, w.LB_SETITEMDATA, index, data );
	
	pop( edx );
	pop( ecx );
	pop( eax );

_end( wListBox_t, set_itemData );



// load_dir-
//
//	Populates a list box with a directory listing.

method wListBox_t.load_dir
( 
	pathname	:string; 
	attributes	:dword 
);;
_begin( wListBox_t, load_dir );

	push( eax );
	push( ecx );
	push( edx );
	
	w.SendMessage( this.handle, w.LB_DIR, attributes, pathname );
		
	pop( edx );
	pop( ecx );
	pop( eax );

_end( wListBox_t, load_dir );



method wListBox_t.processMessage
( 
	hwnd	:dword; 
	uMsg	:dword; 
	wParam	:dword; 
	lParam	:dword 
);
var
	hdc	:dword;
	
_begin( wListBox_t, processMessage );

	// Handle list box notifications here:
	
	if( uMsg = w.WM_CTLCOLORLISTBOX ) then

		// If we've got a ctlcolorbtn message,
		// set the text and background colors.
		
		w.SelectObject( wParam, this.wVisual_private.bkgBrush );
		w.SetTextColor( wParam, this.wListBox_private.textColor );
		w.SetBkColor( wParam, this.wVisual_private.bkgColor );
		mov( this.wVisual_private.bkgBrush, eax );	
		exit processMessage;
				
	endif;
					
	movzx( (type word wParam[2]), eax );
	switch( eax )
	
		case( w.LBN_SELCHANGE )
		
			if( this.wClickable_private.onClick <> NULL ) then
			
				this.wClickable_private.onClick( esi, wParam, lParam );
				
			endif;
			
		case( w.LBN_DBLCLK )
		
			if( this.wClickable_private.onDblClick <> NULL ) then
			
				this.wClickable_private.onDblClick( esi, wParam, lParam );
				
			endif;
			
		default
			
			w.DefWindowProc( hwnd, uMsg, wParam, lParam );
			
	endswitch;

_end( wListBox_t, processMessage );


end	wListBox_u;