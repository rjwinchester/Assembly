unit wPushButton_u;
?@NoDisplay := true;
?@NoStackAlign := true;

#include( "howl.hhf" )
#include( "excepts.hhf" )
#include( "strings.hhf" )
#include( "memory.hhf" )

#includeOnce( "../include/howlunit.hhf" )		
#includeOnce( "stdio.hhf" )
#includeOnce( "stdout.hhf" )

readonly		
	align( 4 );
	vmt( wPushButton_t );

/////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////
//
//
// Handle wPushButton_t objects here:



procedure wPushButton_t.create_wPushButton
(
	wpbName	:string;
	caption	:string;
	parent	:dword; 									
	x		:dword; 
	y		:dword; 
	width	:dword; 
	height	:dword;
	onClick	:widgetProc  
);;
	
_begin( wPushButton_t, create_wPushButton );
	
	push( eax );
	push( ecx );
	push( edx );
	
	if( esi = NULL ) then
	
		mem.alloc( @size( wPushButton_t ));
		mov( eax, esi );
		mov( true, cl );
		
	else
	
		mov( this.wBase_private.onHeap, cl );
		
	endif;

	// We need to create a window that will hold this object.

	(type wButton_t [esi]).create_wButton
	(
		wpbName,
		parent,									
		x,		
		y,		
		width,	
		height,	
		onClick	  
	);	
	mov( cl, this.wBase_private.onHeap );
	w.CreateWindowEx
	(
		0,
		"button",							// Button class name
		caption,							// Button caption
		w.WS_CHILD | w.WS_VISIBLE | w.BS_PUSHBUTTON | w.BS_NOTIFY |
			w.BS_PUSHLIKE | w.WS_TABSTOP | w.WS_GROUP,
		x,
		y,
		width,
		height,
		parent,
		this.wBase_private.objectID,
		howl.howlInstance,
		NULL
	);
	mov( eax, this.handle );
	w.SetWindowLong( eax, w.GWL_USERDATA, esi );
	
	// Initialize the type string field:
		
	howl.setType( wPushButton_c );
	
	// Initialize the VMT pointer:
	
	mov( &wPushButton_t._VMT_, this._pVMT_ );
		
	pop( edx );
	pop( ecx );
	pop( eax );
	
_end( wPushButton_t, create_wPushButton );





end	wPushButton_u;