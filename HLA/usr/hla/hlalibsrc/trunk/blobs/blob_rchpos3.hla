
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit blobUnit;
?@nodisplay := true;
?@nostackalign := true;

#include( "blobs.hhf" );
#include( "excepts.hhf" );
#includeOnce( "threads.hhf" )


/**********************************************************/
/*                                                        */
/* rchpos3-                                               */
/*                                                        */
/* Similar to rchpos2 except that this procedure provides */
/* an extra parameter to let you specify a starting index */
/* (at the end of the blob) to start scanning backwards   */
/* from.                                                  */
/*                                                        */
/**********************************************************/




procedure blob.rchpos3( src1:blob.blob; offs:dword; src2:char ); 
	@noframe;
var
	ecxSave		:dword;
	ediSave		:dword;
	saveFlags	:dword;
	
begin rchpos3;

	push( ebp );
	mov( esp, ebp );
	sub( (_vars_-4), esp );
	pushfd();	// Allocate and initialize saveFlags
	cld();
	
	mov( ecx, ecxSave );
	mov( edi, ediSave );
	
	mov( src1, edi );
	test( edi, edi );
	jz AttemptToDerefNULL;

	#if( @thread )
	try
	
		thread.enterCriticalSection( (type blob.blobRec [edi]).criticalSection );
	
	#endif
	
		// If there are any characters in this blob, scan from the
		// end of the blob to the beginning of the blob looking
		// for the specified character.
		
		mov( (type blob.blobRec [edi]).length, eax );
		mov( offs, ecx );
		cmp( ecx, eax );
		jae ValueInRange;
		
			raise( ex.ValueOutOfRange );
			
		ValueInRange:
		
		test( eax, eax );
		jz notFound;
		
			add( eax, edi );			// Pt at end of blob.
			neg( ecx );
			add( eax, ecx );			// Compute # of chars to scan.
			dec( edi );					// Back off from indexed char.
			mov( src2, al );
			std();						// Search backwards!
			repne.scasb();				// Do the search.
			jnz notFound;
			
				// Found the char in the blob.

				sub( src1, edi );		// Compute index+1 (edi was dec'd!).
				lea( eax, [edi+1] );	// Return index in eax.
				jmp Found;
				
			
		notFound:		
		mov( -1, eax );		// Deprecated!
			

	Found:
		
	#if( @thread )
	
	  anyexception
	
		mov( src1, edx );
		thread.leaveCriticalSection( (type blob.blobRec [edx]).criticalSection );
		push( saveFlags );
		popfd();
		reraise( eax );
	
	endtry;
	mov( src1, ecx );
	thread.leaveCriticalSection( (type blob.blobRec [ecx]).criticalSection );
		
	#endif
	mov( ecxSave, ecx );
	mov( ediSave, edi );
	popfd();
	leave();
	cmp( eax, -1 );		// Set carry if EAX <> -1 (matched char).
	ret( _parms_ );	
	
AttemptToDerefNULL:
	raise( ex.AttemptToDerefNULL );

end rchpos3;

end blobUnit;




	

