
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit blobUnit;
?@nodisplay := true;
?@nostackalign := true;

#include( "blobs.hhf" )
#include( "excepts.hhf" )
#include( "threads.hhf" )



/*******************************************************************/
/*                                                                 */
/* length-                                                         */
/*                                                                 */
/* Returns the current length of the blob passed as a parameter.   */
/*                                                                 */
/*******************************************************************/

procedure blob.length( b:blob.blob ); @noframe;
#if( @thread )
	var
		blen:dword;
#endif
begin length;

	push( ebp );
	mov( esp, ebp );
	#if( @thread )
		sub( _vars_, esp );
	#endif;


	mov( b, eax );
	test( eax, eax );
	jz AttemptToDerefNULL;
	
	#if( @thread )
	
		try
		
			thread.enterCriticalSection
			( 
				(type blob.blobRec [eax]).criticalSection 
			);
			push( (type blob.blobRec [eax]).length );
			pop( blen );
			
		  anyexception
		
			mov( b, edx );
			thread.leaveCriticalSection
			( 
				(type blob.blobRec [edx]).criticalSection 
			);
			reraise( eax );
			
		endtry;
		thread.leaveCriticalSection( (type blob.blobRec [eax]).criticalSection );
		mov( blen, eax );
	
	#else
	
		mov( (type blob.blobRec [eax]).length, eax );
			
	#endif

	leave();
	ret( _parms_ );
	
AttemptToDerefNULL:
	raise( ex.AttemptToDerefNULL );

end length;



end blobUnit;




	

