
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit patterns;
#includeOnce( "../include/pat.hhf" )
#includeOnce( "patterns.hhf" )

	
	

/**********************************************************************/
/*                                                                    */
/* MatchiWord-                                                        */
/*                                                                    */
/* Just like MatchWord, except this does a case insensitive match.	  */
/*                                                                    */
/**********************************************************************/




procedure pat.matchiWord( s:string ); @nodisplay; @noframe;
begin matchiWord;

	push( ebp );		// must do this ourselves since noframe
	mov( esp, ebp );	// is specified as an option.
	cld();
	mov( esi, ebx );
	
	mov( s, edx );
	mov( (type str.strRec [edx]).length, ecx );
	mov( edi, eax );
	sub( esi, eax );
	
	// Are there enough characters to compare?
	
	cmp( ecx, eax );
	ja fail1;
	test( ecx, ecx );
	jz whileDone;
	whileLoop:

		mov( [esi], al );
		UCase( al );
		mov( [edx], ah );
		UCase( ah );
		cmp( al, ah );
		jne fail2;
		inc( esi );
		inc( edx );
		dec( ecx );
		jnz whileLoop;

	whileDone:


	// The character immediately after the last character
	// matched must be a member of the WordDelims cset, or
	// we need to be at the end of the string.

	cmp( esi, edi );
	jae success;
	
		xor( eax, eax );
		mov( [esi], al );
		bt( eax, (type dword pat.WordDelims) );
		jnc fail2;
		
	success:
		
	// Since this routine doesn't have to handle backtracking,
	// a simple return indicates success.

	pop( ebp );
	ret( _parms_ );
		
	
fail2:	
	mov( ebx, esi );
fail1:
	pat._fail_( pat.FailTo );

end matchiWord;


end patterns;
