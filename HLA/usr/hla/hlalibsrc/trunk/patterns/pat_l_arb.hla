
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit patterns;
#includeOnce( "../include/pat.hhf" )
#includeOnce( "patterns.hhf" )
#includeOnce( "misctypes.hhf" )

	
	

/**************************************************************/
/*                                                            */
/* l_arb-			                                          */
/*                                                            */
/* Lazily matches all the remaining characters 				  */
/* in a string.  Relies on backtracking for any following     */
/* patterns to match.                                         */
/* 							                                  */
/*                                                            */
/* Disposition:		Lazy                                      */
/* BackTrackable:	Yes                                       */
/*                                                            */
/* Entry Parameters:                                          */
/*                                                            */
/* 	ESI:	Pointer to sequence of characters to match.       */
/* 	EDI:	Pointer to byte beyond last char to match.        */
/*                                                            */
/* Exit Parameters (if success):                              */
/*                                                            */
/*  EBX:	Points at the start of matched sequence.		  */
/* 	ESI:	Unchanged.										  */
/* 														      */
/* 	EDI:	Unchanged from entry value.                       */
/*                                                            */
/* Exit Parameters (if failure):                              */
/* 														      */
/* EDI:	Unchanged from original entry value.			      */
/* ESI:	Unchanged from original entry value.			      */
/*                                                            */
/* Unless noted, assume all other registers can be modified   */
/* by this code.                                              */
/*                                                            */
/**************************************************************/


		
		
procedure pat.l_arb; @nodisplay; @noalignstack; @noframe;

var
	cursor: 	misc.pChar;		// Save last matched posn here.
	startPosn:	misc.pChar; 	// Save start of str here.
	endStr:		misc.pChar;		// End of string goes here.
	espSave:	dword;			// To clean stk after back trk.
	FailToSave:	pat.FailRec;	// Save global FailTo value here.
	
begin l_arb;

	push( ebp );
	mov( esp, ebp );
	sub( _vars_, esp );

	// If some routine after this one fails and transfers
	// control via backtracking to this code, the stack
	// will be a mess.  So save esp so we can clean up
	// the stack if backtracking is necessary.
	
	mov( esp, espSave );
	
	// Save pointer to end of string to match.
	// This is needed to restore this value when
	// backtracking occurs.

	mov( edi, endStr );

	// Save a pointer to the start of the string.  Then
	// succeed without matching anything.

	mov( esi, startPosn );
	mov( esi, cursor );
	mov( esi, ebx );
	
	pat._success_( FailToSave, FailToHere );




	// If someone after us fails and invokes
	// backtracking, control is transfered to
	// this point.  First, we need to restore
	// ESP to clean up the junk on the stack.
	// Then we need to see if we match the
	// current character and skip it if we do.
	
	FailToHere:

		mov( espSave, esp );		// Clean up stack.
		
		mov( cursor, esi );			// Get last posn we matched.
		inc( esi );					// Advance by one position.
		mov( endStr, edi );			// Restore to original value.

		// If we've exceeded the maximum limit on the string,
		// then fail.

		cmp( esi, endStr );
		jae fail3;

		// If we drop down here, there is at least one
		// character left in the string so call the next 
		// matching routine (by jumping to the return address) 
		// to continue the pattern match.
		
		mov( startPosn, ebx );
		mov( esi, cursor );				// Save in case caller fails.
		mov( [ebp+4], eax );			// and call next routine.
		mov( [ebp], ebp );
		jmp( eax );


fail3:
	// Need to restore FailTo address because it
	// currently points at us.  We want to jump
	// to the correct location.

	mov( startPosn, esi );
	mov( FailToSave.ebpSave, pat.FailTo.ebpSave );
	mov( FailToSave.jmpAdrs, pat.FailTo.jmpAdrs );
	pat._fail_( pat.FailTo );

	
end l_arb;


		






end patterns;
