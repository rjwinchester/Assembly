// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/7/2008, by Randall Hyde


unit sockets;
?@nostackalign := true;
?@noframe := true;
?@nodisplay := true;

#include( "sockets.hhf" )
#include( "excepts.hhf" )
#include( "os.hhf" )

#if( os.win32 )

	// Note: must explicitly include wsock32.lib under Windows

	#include( "w.hhf" )

#elseif( os.linux )
	#include( "linux.hhf" )
#elseif( os.freeBSD )

	#include( "bsd.hhf" )
	procedure _setsockopt
	( 
			s		:dword; 
			level	:dword;
			optname	:dword;
		var	optval	:var;
			optlen	:hla.socklen_t 
	);
		@cdecl;
		@external( "setsockopt" );
	
#elseif( os.macOS )
	#include( "mac.hhf" )
#else
	#error( "setsockopt.hla: unsupported OS" )
#endif


procedure sock.setsockopt
( 
		s		:dword; 
		level	:dword;
		optname	:dword;
	var	optval	:var;
		optlen	:hla.socklen_t 
);
begin setsockopt;

	push( ebp );
	mov( esp, ebp );
	push( eax );
	
	#if( os.win32 )
	
		push( ecx );
		push( edx );
		
		w.setsockopt( s, level, optname, val optval, optlen );
		cmp( eax, w.SOCKET_ERROR );
		je WinSocketError;
		
		pop( edx );
		pop( ecx );
		
	#elseif( os.linux )
	
		linux.setsockopt( s, level, optname, val optval, optlen );
		test( eax, eax );
		js LinuxSocketError;
		
	#elseif( os.freeBSD )
	
		push( ecx );
		push( edx );
		
		_setsockopt( s, level, optname, val optval, optlen );
		add( 20, esp );
		cmp( eax, -1 );
		je SocketError;
		
		pop( edx );
		pop( ecx );
		
	#elseif( os.macOS )
	
		mac.setsockopt( s, level, optname, val optval, optlen );
		jc SocketError;
		
	#else
	
		#error( "Unknown OS!" );
		
	#endif	
	pop( eax );
	leave();
	ret( _parms_ );
	
#if( os.win32 )
WinSocketError:
	w.WSAGetLastError();
	mov( eax, edx );
	
#elseif( os.linux )

LinuxSocketError:
	neg( eax );			// Linux error code.
	mov( eax, edx );
	
#endif
	
SocketError:
	raise( ex.SocketError );
	
end setsockopt;


end sockets;
