
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit hlaUnit;
?@nostackalign := true;
?@noframe := true;
?@nodisplay := true;

#include( "hla.hhf" )
#include( "conv.hhf" )
#include( "excepts.hhf" )



// hla.geti128
//
//	Reads a 128-bit integer from a stream.
// Stores the result into the lword pointed at by l.


procedure hla.geti128( var l:lword; getc:thunk ); 
	@noframe; 
	@nodisplay;

var
	eaxSave		:dword;
	ebxSave		:dword;
	ecxSave		:dword;
	edxSave		:dword;
	ediSave		:dword;
	esiSave		:dword;
	Delimiters	:cset;
	IsNeg		:boolean;
	
begin geti128;

	push( ebp );
	mov( esp, ebp );
	sub( _vars_, esp );
	
	mov( eax, eaxSave );
	mov( ebx, ebxSave );
	mov( ecx, ecxSave );
	mov( edx, edxSave );
	mov( edi, ediSave );
	mov( esi, esiSave );

	conv.getDelimiters( Delimiters );

	xor( eax, eax );
	xor( edi, edi );
	xor( edx, edx );
	xor( ecx, ecx );
	xor( ebx, ebx );
	
	// Skip over any leading delimiters:
	
	skipDelims:
	
		getc();
		jc convErr;
		cmp( al, $80 );
		jae illegalChar;
		bt( eax, (type dword Delimiters ));
		jc skipDelims;
		
	
	
	// Process a possible leading minus sign:
	
	mov( false, IsNeg );
	cmp( al, '-' );
	jne notNegative;
	
		mov( true, IsNeg );
		getc();
		jc convErr;
		cmp( al, $80 );
		jae illegalChar;
		
	notNegative:
	
		
	// Okay, input the digits and convert to a number:
	
	
	whileADigit:
	
		// The character must be a numeric digit.
		
		cmp( al, '0' );
		jb convErr;
		cmp( al, '9' );
		ja convErr;			


		shl( 1, ebx );	// Multiply edi:edx:ecx:ebx by 10.
		rcl( 1, ecx );
		rcl( 1, edx );
		rcl( 1, edi );
		jc Overflow;
			
		push( edi );
		push( edx );	// Save *2.
		push( ecx );
		push( ebx );
		
		shl( 1, ebx );	// Compute *4
		rcl( 1, ecx );
		rcl( 1, edx );
		rcl( 1, edi );
		jc Overflow;
		
		shl( 1, ebx );	// Compute *8
		rcl( 1, ecx );
		rcl( 1, edx );
		rcl( 1, edi );
		jc Overflow;
		
		and( $f, eax );	// Add in current digit.
		add( eax, ebx );
		adc( 0, ecx );
		adc( 0, edx );
		adc( 0, edi );
		jc Overflow;
		
		add( [esp], ebx );
		adc( [esp+4], ecx );
		adc( [esp+8], edx );
		adc( [esp+12], edi );
		jc Overflow;
		
		add( 16, esp );		// Remove *2 from stack.
		
		// Skip any underscores in the middle of the number.
		// (Technically, this allows underscores at the end
		// of the number too, but we'll ignore this issue).
				
		repeatUntilALne_:
		
			getc();
			jc EOF;
			
			cmp( al, $80 );
			jae illegalChar;
			
			cmp( al, '_' );
			je repeatUntilALne_;
			
			movzx( al, eax );
			bt( eax, (type dword Delimiters ));
			jnc whileADigit;

	EOF:	
	
	// Up to this point, we've only checked for unsigned overflow.
	// We couldn't check for signed overflow because they may be entering
	// the minimum negative # that has no positive counterpart. So at this
	// point we've got to check for signed overflow.
	
	cmp( edi, $8000_0000 );
	ja Overflow;
	jb noOverflow;
	
		mov( edx, eax );
		or( ecx, eax );
		or( ebx, eax );
		jnz Overflow;

		// At this point, the value is $8000_0000_0000_0000_0000_0000_0000_0000
		// which is valid only if the number is negative.
		
		cmp( IsNeg, false );
		je Overflow;	
	
	noOverflow:
	
	// Okay, overflow has not occured, so let's negate the number if there
	// was a sign.
	
	mov( ecx, eax );
	cmp( IsNeg, false );
	je DontNegate;
	
		// IsNeg is true, so negate the 128-bit value:
			
		xor( eax, eax );
		sub( ebx, eax );
		
		mov( 0, ebx );
		sbb( ecx, ebx );
		
		mov( 0, ecx );
		sbb( edx, ecx );
		
		mov( 0, edx );
		sbb( edi, edx );
		
		mov( l, edi );
		mov( eax, [edi] );
		mov( ebx, [edi+4] );
		mov( ecx, [edi+8] );
		mov( edx, [edi+12] );
		jmp negationDone;
		
	DontNegate:
	
		mov( l, eax );
		mov( ebx, [eax] );
		mov( ecx, [eax+4] );
		mov( edx, [eax+8] );
		mov( edi, [eax+12] );
		
	negationDone:
	
	mov( eaxSave, eax );
	mov( ebxSave, ebx );
	mov( ecxSave, ecx );
	mov( edxSave, edx );
	mov( ediSave, edi );
	mov( esiSave, esi );
	leave();
	ret( _parms_ );

convErr:
	raise( ex.ConversionError );

Overflow:
	raise( ex.ValueOutOfRange );
		
illegalChar:
	raise( ex.IllegalChar );
	
end geti128;
	
end hlaUnit;

