
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 8/17/2006, by Randall Hyde


unit ConvUnit;

#includeOnce( "../include/conversions.hhf" )
#includeOnce( "conv.hhf" )
#includeOnce( "stdlibdata.hhf" )
#includeOnce( "excepts.hhf" )
#includeOnce( "stdlibdata.hhf" )

/*************************************************************************/
/*                                                                       */
/* _hexToBuf80-                                                          */
/*                                                                       */
/* On entry:                                                             */
/* 	tb contains a numeric value to convert to a hexadecimal string.      */
/*  width contains the number of digits to print (from L.O.->H.O.).      */
/* 	                                                                     */
/* 	EDI points at the end of a memory buffer large enough to hold an     */
/* 	80-bit hexadecimal value (at least 20 if OutputUnderscores is false, */
/*  at least 24 bytes if OutputUnderscores is true).                     */
/*                                                                       */
/* On exit:                                                              */
/* 	The buffer will contain a string that is the                         */
/* 	hexadecimal representation of the value and EDI will point at the    */
/* 	start of the string. If OutputUnderscores is true this routine will  */
/* 	emit an underscore between groups of four hexadecimal digits.        */
/*                                                                       */
/*************************************************************************/





procedure conv._hexToBuf80
( 
		tb		:tbyte;
		width	:dword; 
	var buffer	:char in edi 
);
	@noframe; 
	@nodisplay; 
	@noalignstack;

var
	edxSave	:dword;				// These are organized so that the MOV
	ecxSave	:dword;				//  instructions below access these
	ebxSave	:dword;				//  variables from lowest address to
	eaxSave	:dword;				//  highest address (better for cache).

readonly

	// "hexToBuffer" jump tables:

	htb80noUS	:dword[21] :=
				[
					&badWidth,
					&noUS1,
					&noUS2,
					&noUS3,
					&noUS4,
					&noUS5,
					&noUS6,
					&noUS7,
					&noUS8,
					&noUS9,
					&noUS10,
					&noUS11,
					&noUS12,
					&noUS13,
					&noUS14,
					&noUS15,
					&noUS16,
					&noUS17,
					&noUS18,
					&noUS19,
					&noUS20
				];

	htb80hasUS	:dword[25] :=
				[
					&badWidth,
					&hasUS1,
					&hasUS2,
					&hasUS3,
					&hasUS4,
					&badWidth,
					&hasUS6,
					&hasUS7,
					&hasUS8,
					&hasUS9,
					&badWidth,
					&hasUS11,
					&hasUS12,
					&hasUS13,
					&hasUS14,
					&badWidth,
					&hasUS16,
					&hasUS17,
					&hasUS18,
					&hasUS19,
					&badWidth,
					&hasUS21,
					&hasUS22,
					&hasUS23,
					&hasUS24
				];



	#macro emitXDigit( src, digit, posn );
		
		mov( (type dword src), ebx );
		shr( digit*4, ebx );
		and( $f, ebx );						// Strip out unwanted bits.
		mov( stdlib.hexDigits[ebx], dl );	// Convert digit to hex char.
		mov( dl, [edi-posn] );
		 
	#endmacro


begin _hexToBuf80;

	push( ebp );
	mov( esp, ebp );
	sub( _vars_, esp );
	mov( eax, eaxSave );			// Intel recommends MOVs rather
	mov( ebx, ebxSave );			// than pushes and pops.
	mov( ecx, ecxSave );
	mov( edx, edxSave );
		
	mov( width, ecx );

	// Handle output with underscores later in this file:
	
	_hla.test_OutputUnderscores();
	jne DoUnderscores;

	// Drop down here if we're not outputting underscores inbetween groups of
	// four digits in the number.
	//
	// Max width is 20 character positions:

	cmp( ecx, 20 );
	ja badWidth;

	// Jump to one of the following labels based on the
	// output size of the number:

	jmp( htb80noUS[ ecx*4 ] );

	noUS20:
		emitXDigit(tb[8],3,19);
	noUS19:
		emitXDigit(tb[8],2,18);
	noUS18:
		emitXDigit(tb[8],1,17);
	noUS17:
		mov( (type dword tb[8]), ebx );
		and( $f, ebx );						// Strip out unwanted bits.
		mov( stdlib.hexDigits[ebx], dl );	// Convert digit to hex char.
		mov( dl, [edi-16] );


	noUS16:
		emitXDigit(tb[4],7,15);
	noUS15:
		emitXDigit(tb[4],6,14);
	noUS14:
		emitXDigit(tb[4],5,13);
	noUS13:
		emitXDigit(tb[4],4,12);
	noUS12:
		emitXDigit(tb[4],3,11);
	noUS11:
		emitXDigit(tb[4],2,10);
	noUS10:
		emitXDigit(tb[4],1,9);
	
	noUS9:
		mov( (type dword tb[4]), ebx );
		and( $f, ebx );						// Strip out unwanted bits.
		mov( stdlib.hexDigits[ebx], dl );	// Convert digit to hex char.
		mov( dl, [edi-8] );
	
	noUS8:
		emitXDigit(tb,7,7);
	noUS7:
		emitXDigit(tb,6,6);
	noUS6:
		emitXDigit(tb,5,5);
	noUS5:
		emitXDigit(tb,4,4);
	noUS4:
		emitXDigit(tb,3,3);
	noUS3:
		emitXDigit(tb,2,2);
	noUS2:
		emitXDigit(tb,1,1);
	noUS1:
		mov( (type dword tb), ebx );
		and( $f, ebx );						// Strip out unwanted bits.
		mov( stdlib.hexDigits[ebx], dl );	// Convert digit to hex char.
		mov( dl, [edi] );

		sub( ecx, edi );					// Point edi at start of buffer + 1
	jmp htbDone;
	
		
		
// Version of the above code that emits underscores between
// every four digits.  Yep, repeated code (ugly), but done
// because the underscore processing is slower.
		
DoUnderscores:

	// Drop down here if we're not outputting underscores inbetween groups of
	// four digits in the number.
	//
	// Max width is 24 character positions:

	cmp( ecx, 24 );
	ja badWidth;

	// Jump to one of the following labels based on the
	// output size of the number:

	jmp( htb80hasUS[ ecx*4 ] );



	hasUS24:
		emitXDigit(tb[8],3,23);
	hasUS23:
		emitXDigit(tb[8],2,22);
	hasUS22:
		emitXDigit(tb[8],1,21);
	hasUS21:
		mov( (type dword tb[8]), ebx );
		and( $f, ebx );						// Strip out unwanted bits.
		mov( stdlib.hexDigits[ebx], dl );	// Convert digit to hex char.
		mov( dl, [edi-20] );

		mov( '_', (type char [edi-19]));

	hasUS19:
		emitXDigit(tb[4],7,18);
	hasUS18:
		emitXDigit(tb[4],6,17);
	hasUS17:
		emitXDigit(tb[4],5,16);
	hasUS16:
		emitXDigit(tb[4],4,15);

		mov( '_', (type char [edi-14]));

	hasUS14:
		emitXDigit(tb[4],3,13);
	hasUS13:
		emitXDigit(tb[4],2,12);
	hasUS12:
		emitXDigit(tb[4],1,11);
	hasUS11:
		mov( (type dword tb[4]), ebx );
		and( $f, ebx );						// Strip out unwanted bits.
		mov( stdlib.hexDigits[ebx], dl );	// Convert digit to hex char.
		mov( dl, [edi-10] );
		
		mov( '_', (type char [edi-9]));


	hasUS9:
		emitXDigit(tb,7,8);
	hasUS8:
		emitXDigit(tb,6,7);
	hasUS7:			
		emitXDigit(tb,5,6);
	hasUS6:
		emitXDigit(tb,4,5);
		
		mov( '_', (type char [edi-4]));

	hasUS4:
		emitXDigit(tb,3,3);
	hasUS3:
		emitXDigit(tb,2,2);
	hasUS2:
		emitXDigit(tb,1,1);
	hasUS1:
		mov( (type dword tb), ebx );
		and( $f, ebx );						// Strip out unwanted bits.
		mov( stdlib.hexDigits[ebx], dl );	// Convert digit to hex char.
		mov( dl, [edi] );

		sub( ecx, edi );					// Point edi at start of buffer + 1
	
htbDone:
	add( 1, edi );			// Point back at first char in buffer.
	mov( eaxSave, eax );
	mov( ebxSave, ebx );
	mov( ecxSave, ecx );
	mov( edxSave, edx );
	leave();
	ret( _parms_ );

badWidth:
	raise( ex.WidthTooBig );

end _hexToBuf80;

end ConvUnit;
