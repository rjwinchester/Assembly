// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit ConvUnit;

#includeOnce( "../include/conversions.hhf" )
#includeOnce( "conv.hhf" )
#includeOnce( "strings.hhf" )
#includeOnce( "memory.hhf" )


/**********************************************************************/
/*                                                                    */
/* a_u128ToStr-                                                       */
/*                                                                    */
/* Just like u128ToStr except this function allocates storage for the */
/* string on the heap and returns a pointer to the string in EAX.     */
/*                                                                    */
/**********************************************************************/


procedure conv.a_u128ToStr( l:lword; width:int32; fill:char );
	 @nodisplay;
	 @noframe; 
	 @noalignstack;
var
	ediSave	:dword;
	esiSave	:dword;
	edxSave	:dword;
	ecxSave	:dword;
	eaxSave	:dword;

	dWidth	:dword;
	bufSize	:dword;
	
begin a_u128ToStr;

	push( ebp );
	mov( esp, ebp );
	sub( _vars_, esp );
	mov( ecx, ecxSave );
	mov( edx, edxSave );
	mov( esi, esiSave );
	mov( edi, ediSave );

	mov( width, eax );
	test( eax, eax );
	jns sizeNotNegative;

		neg( eax );

	sizeNotNegative:
	mov( eax, bufSize );

	// Compute the size of the resulting string and check to
	// see if it will exceed the maximum length of the supplied
	// string variable.
	//
	// Compute the number of print positions the number requires.
	
	conv.u128Size( l );
	mov( eax, dWidth );		// Save # of print positions required.

	// Determine the larger of the computed print positions
	// and the minimum size value.

	cmp( eax, bufSize );
	cmovb( bufSize, eax );
	mov( eax, bufSize );
	mov( eax, ecx );
	str.alloc( eax );
	mov( eax, esi );
	mov( eax, eaxSave );
	
	// Note that, at this point, EAX contains the number of print
	// positions we will use (including underscores).  Save this
	// as the ultimate length of the string.
	
	mov( ecx, (type str.strRec [esi]).length );

	
	movzx( fill, eax );
	conv._unsToBuf128Size
	( 
		l, 
		width, 
		dWidth, 
		al, 
		[esi], 
		bufSize
	);
	mov( 0, (type byte [edi+ecx]));	

	mov( eaxSave, eax );
	mov( ecxSave, ecx );
	mov( edxSave, edx );
	mov( esiSave, esi );
	mov( ediSave, edi );
	leave();
	ret( _parms_ );
	 

end a_u128ToStr;

end ConvUnit;
