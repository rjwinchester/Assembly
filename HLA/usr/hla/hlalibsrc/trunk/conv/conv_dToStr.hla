
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit ConvUnit;

#includeOnce( "../include/conversions.hhf" )
#includeOnce( "stdlibdata.hhf" );
#includeOnce( "conv.hhf" )
#includeOnce( "excepts.hhf" )
#includeOnce( "strings.hhf" )

/************************************************************/
/*                                                          */
/* dToStr-                                                  */
/*                                                          */
/* Converts a dword value to a string of hexadecimal digits.*/
/*                                                          */
/* Inputs:                                                  */
/* 	d-	                                                    */
/* 		contains the dword to convert.                      */
/* 		                                                    */
/* 	buffer-                                                 */
/* 		points at an HLA string that will hold              */
/* 		the converted result.                               */
/*                                                          */
/************************************************************/


procedure conv.dToStr( d:dword; buffer:string ); 
	@nodisplay;
	@noframe;
	@noalignstack;
	
begin dToStr;

	push( ebp );
	mov( esp, ebp );
	push( eax );
	push( edi );
	
	mov( buffer, edi );
	test( edi, edi );
	jz refNULL;
	
	// Compute the length of the string (including an underscore,
	// if it turns out to be necessary):
	
	conv.getUnderscores();
	add( 8, eax );
	
	cmp( eax, (type str.strRec [edi]).maxlen );
	ja strOvfl;
	
	mov( eax, (type str.strRec [edi]).length );
	cmp( eax, 8 );
	je noUnderscores;
	
		// Create the 9 character string from the hex value:
		

		movzx( (type byte d[3]), eax );
		mov( (type word stdlib.hexTbl[eax*2]), ax );
		mov( ax, [edi] );

		movzx( (type byte d[2]), eax );
		mov( (type word stdlib.hexTbl[eax*2]), ax );
		mov( ax, [edi+2] );
		
		mov( '_', (type byte [edi+4]));
		
		movzx( (type byte d[1]), eax );
		mov( (type word stdlib.hexTbl[eax*2]), ax );
		mov( ax, [edi+5] );
		
		movzx( (type byte d[0]), eax );
		mov( (type word stdlib.hexTbl[eax*2]), ax );
		mov( ax, [edi+7] );
		mov( 0, (type byte [edi+9]));
		jmp allDone;
		
		
	
		// Create the 8 character string (no underscores) from the hex value:
		
	noUnderscores:

		movzx( (type byte d[3]), eax );
		mov( (type word stdlib.hexTbl[eax*2]), ax );
		mov( ax, [edi] );

		movzx( (type byte d[2]), eax );
		mov( (type word stdlib.hexTbl[eax*2]), ax );
		mov( ax, [edi+2] );
		
		movzx( (type byte d[1]), eax );
		mov( (type word stdlib.hexTbl[eax*2]), ax );
		mov( ax, [edi+4] );
		
		movzx( (type byte d[0]), eax );
		mov( (type word stdlib.hexTbl[eax*2]), ax );
		mov( ax, [edi+6] );
		mov( 0, (type byte [edi+8]));
		
allDone:	
	pop( edi );
	pop( eax );
	pop( ebp );
	ret( _parms_ );
	
strOvfl:
	raise( ex.StringOverflow );
	
refNULL:
	raise( ex.AttemptToDerefNULL );
	
end dToStr;

end ConvUnit;
