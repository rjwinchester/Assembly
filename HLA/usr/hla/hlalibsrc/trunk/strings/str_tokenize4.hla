
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit stringUnit;
?@nodisplay := true;
?@nostackalign := true;

#include( "strings.hhf" );
#include( "excepts.hhf" );
#include( "memory.hhf" );
#include( "stdio.hhf" );



/*************************************************************/
/*                                                           */
/* tokenize-                                                 */
/*                                                           */
/* dest points at an array of strings (that is, an array     */
/* of pointers to strings).  src points at a single string.  */
/* This routine scans the strings and extracts "words" from  */
/* the string.  Each extracted string is allocated storage   */
/* via stralloc and a pointer to that string is stored in    */
/* successive locations of the dest array.                   */
/*                                                           */
/* This routine defines a word as any sequence of characters */
/* surrounded by the following:                              */
/*                                                           */
/* (1) the beginning of the src string,                      */
/* (2) the end of the src string,                            */
/* (3) any of the delimiters: space, tab, comma, "<", ">",   */
/*     "|", "/", "\", or "-".                                */
/* 	                                                         */
/* This routine is typically used to prepare the "ARGV"      */
/* array in preparation for parsing a command line parameter */
/* set.                                                      */
/*															 */
/* Returns:													 */
/*															 */
/* Set of zero or more string pointers in the dest array.	 */
/* Number of words found in EAX.							 */
/*															 */
/*                                                           */
/*************************************************************/



procedure str.tokenize4
(
		src		:string;
		delims	:cset;
	var	dest	:var;
		maxStrs	:dword
);  @noframe;
begin tokenize4;

	push( ebp );
	mov( esp, ebp );
	push( ebx );
	push( ecx );
	push( esi );
	push( edi );
	push( edx );
	
	
	// #0 (end of string) is always a delimiter.
	// Force that bit on in the delimiter character set.
	// Note that this only affects the local copy passed
	// as a parameter.
	
	or( 1, (type byte delims));
	
	mov( src, esi );
	mov( dest, edi );
	test( esi, esi );
	jz AttemptToDerefNULL;
	test( edi, edi );
	jz AttemptToDerefNULL;
	mov( maxStrs, edx );
	lea( edx, [edi+edx*4] );	// Compute adrs of end of dest array.
	
	// As long as we don't hit the end of the string,
	//  extract the words from it.
	
	xor( ecx, ecx );
	cmp( (type char [esi]), #0 );
	je endWhl;
	whlLoop:
		cmp( edi, edx );		// Gone beyond the end of the array?
		jae ArrayBounds;
	
		// Skip over all leading delimiter characters:
		
		xor( eax, eax );
		dec( esi );
		SkipDelims:
		
			// Since csets only support the 7-bit character set,
			// strip off the H.O. bit of the current byte.  Note
			// the implication that $80 is always a delimiter char.
			//
			// Repeat as long as the current character is not a
			// zero byte and is a delimiter character.
			
			inc( esi );
			mov( [esi], al );
			cmp( al, 0 );
			je endWhl;

			and( $7f, al );
			bt( eax, (type dword delims) );
			jc SkipDelims;
			
		
		// Okay, we're at the beginning of a new word.  Find the
		// end of the word.
		//
		// Note the special case: if the quote is not in the
		// delimiter set (we would skip over it in the above
		// loop if it were), then we grab all characters up
		// to the matching end quote.

		cmp( al, '"' );
		jne tryApost;
		
			inc( esi );				// Don't include the quote in the string.
			mov( esi, ebx );
			dec( esi );
			WhileNotQuote:
			
				inc( esi );
				mov( [esi], al );
				cmp( al, 0 );
				je endQuoted;
				cmp( al, '"' );
				je WhileNotQuote;
				jmp endQuoted;	
			
		tryApost:
		cmp( al, '''' );
		jne notQuoted;		
		
			inc( esi );				// Don't include the apost in the string.
			mov( esi, ebx );
			dec( esi );
			WhileNotApost:
			
				inc( esi );
				mov( [esi], al );
				cmp( al, 0 );
				je endQuoted;
				cmp( al, '''' );
				jne WhileNotApost;
				jmp endQuoted;	
		
		notQuoted:
		
			mov( esi, ebx );		// Save ptr to start of word.
			dec( esi );
			repeatUntilDelim:		// Skip to the end of the word.
			
				inc( esi );
				mov( [esi], al );
				bt( eax, (type dword delims)); 
				jnc repeatUntilDelim;
			
		endQuoted:
		
		
		// Okay, we've got a word delimited by ebx and esi.
		// Make a copy of this substring and store a pointer
		// to the substring in the current dest location.
		
		mov( esi, eax );	// Compute length of substring as
		sub( ebx, eax );	//  esi-ebx.
		sub( src, ebx );	// Compute starting index for substring.
		
		str.a_substr( src, ebx, eax );

		mov( eax, [edi] );	// Store away pointer to this word.
		add( 4, edi );		// Bump to next array element.
		inc( ecx );			// Count the number of words here.
		
		// If we stopped scanning on a quote or apostrophe, skip
		// over that symbol here.
		
		mov( [esi], al );
		cmp( al, 0 );
		je noInc;
		cmp( al, '"' );
		je noInc;
		cmp( al, '''' );
		je noInc;
		
			add( 1, esi );
			
		noInc:
		cmp( (type char [esi]), #0 );
		jne whlLoop;

	endWhl:				

	mov( ecx, eax );		// Return word count in EAX.
	pop( edx );
	pop( edi );
	pop( esi );
	pop( ecx );
	pop( ebx );	
	pop( ebp );
	ret( _parms_ );

AttemptToDerefNULL:
	raise( ex.AttemptToDerefNULL );
	
ArrayBounds:
	raise( ex.ArrayBounds );
		 
end tokenize4;




end stringUnit;




	

