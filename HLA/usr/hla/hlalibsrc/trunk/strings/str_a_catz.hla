
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit stringUnit;
?@nodisplay := true;
?@nostackalign := true;

#include( "strings.hhf" )
#include( "excepts.hhf" )
#include( "memory.hhf" )

/**********************************************************/
/*                                                        */
/* a_catz-                                                */
/*                                                        */
/* Concatenates a zero-terminated string to the end of an */
/* HLA string.                                            */
/*                                                        */
/**********************************************************/


procedure str.a_catz( zsrc:zstring; ssrc:string ); 
	@noframe;	
begin a_catz;

	push( ebp );
	mov( esp, ebp );
	push( ecx );
	push( edi );
	push( esi );
	pushfd();
	cld();
	
	// First, compute the length of the zero-terminated string
	// so we can compute the size of the new string.
	
	
	mov( zsrc, ecx );
	test( ecx, ecx );			// Make sure it's a non-NULL pointer
	jz AttemptToDerefNULL;
	
	mov( ecx, eax );
	and( %11, eax );
	jz dwordAligned;
	
		cmp( (type byte [ecx]), 0 );
		jz GotLen;
		add( 1, ecx );
		cmp( eax, %11 );
		je dwordAligned;
	
		cmp( (type byte [ecx]), 0 );
		jz GotLen;
		add( 1, ecx );
		cmp( eax, %10 );
		je dwordAligned;
	
		cmp( (type byte [ecx]), 0 );
		jz GotLen;
		add( 1, ecx );
	
	
	dwordAligned:
	sub( 32, ecx );				// To counteract add immediately below.
	ZeroLoop:
		add( 32, ecx );			// Skip chars this loop just processed.
	ZeroLoop2:
		mov( [ecx], eax );		// Get next four chars into EAX.
		and( $7f7f7f7f, eax );	// Clear H.O. bit (note:$80->$00!)
		sub( $01010101, eax );	// $00 and $80->$FF, all others have pos val.
		and( $80808080, eax );	// Test all H.O. bits.  If any are set, then
		jnz MightBeZero0;		//  we've got a $00 or $80 byte.
		
		mov( [ecx+4], eax );	// The following are all in-line expansions
		and( $7f7f7f7f, eax );	// of the above (we'll process 32 bytes on
		sub( $01010101, eax );	// each iteration of this loop). 
		and( $80808080, eax );
		jnz MightBeZero4;
		
		mov( [ecx+8], eax );
		and( $7f7f7f7f, eax );
		sub( $01010101, eax );
		and( $80808080, eax );
		jnz MightBeZero8;
		
		mov( [ecx+12], eax );
		and( $7f7f7f7f, eax );
		sub( $01010101, eax );
		and( $80808080, eax );
		jnz MightBeZero12;
		
		mov( [ecx+16], eax );
		and( $7f7f7f7f, eax );
		sub( $01010101, eax );
		and( $80808080, eax );
		jnz MightBeZero16;
		
		mov( [ecx+20], eax );
		and( $7f7f7f7f, eax );
		sub( $01010101, eax );
		and( $80808080, eax );
		jnz MightBeZero20;
		
		mov( [ecx+24], eax );
		and( $7f7f7f7f, eax );
		sub( $01010101, eax );
		and( $80808080, eax );
		jnz MightBeZero24;
		
		mov( [ecx+28], eax );
		and( $7f7f7f7f, eax );
		sub( $01010101, eax );
		and( $80808080, eax );
		jz ZeroLoop;
	
	// The following code handles the case where we found a $80
	// or a $00 byte.  We need to determine whether it was a zero
	// byte and the exact position of the zero byte.  If it was a
	// $80 byte, then we've got to continue processing characters
	// in the string.
	 
	
	// Okay, we've found a $00 or $80 byte in positions
	// 28..31.  Check for the location of the zero byte, if any.
		
		add( 28, ecx );
		jmp	MightBeZero0;

	// If we get to this point, we've found a zero byte in
	// positions 4..7:
	
	MightBeZero4:
		add( 4, ecx );
		jmp	MightBeZero0;

	// If we get to this point, we've found a zero byte in
	// positions 8..11:
	
	MightBeZero8:
		add( 8, ecx );
		jmp	MightBeZero0;

	// If we get to this point, we've found a zero byte in
	// positions 12..15:
	
	MightBeZero12:
		add( 12, ecx );
		jmp	MightBeZero0;

	// If we get to this point, we've found a zero byte in
	// positions 16..19:
	
	MightBeZero16:
		add( 16, ecx );
		jmp	MightBeZero0;

	// If we get to this point, we've found a zero byte in
	// positions 20..23:
	
	MightBeZero20:
		add( 20, ecx );
		jmp	MightBeZero0;

	// If we get to this point, we've found a zero byte in
	// positions 24..27:
	
	MightBeZero24:
		add( 24, ecx );

	// If we get to this point, we've found a zero byte in
	// positions 0..3 or we've branched here from one of the
	// above conditions
	
	MightBeZero0:
		mov( [ecx], eax );			// Get the original four bytes.
		cmp( al, 0 );				// See if the first byte contained zero.
		je GotLen;
		inc( ecx );
		cmp( ah, 0 );				// See if the second byte contained zero.
		je GotLen;
		inc( ecx );
		test( $FF_0000, eax );		// See if byte #2 contained a zero.
		je GotLen;
		inc( ecx );
		test( $FF00_0000, eax );	// See if the H.O. byte contained zero.
		je GotLen;
		
	// Well, it must have been a $80 byte we encountered.
	// (Fortunately, they are rare in ASCII strings, so all this
	// extra computation rarely occurs).  Jump back into the zero
	// loop and continue processing.
	
		inc( ecx );		// Skip bytes we just processed.
		jmp ZeroLoop2;	// Don't bother adding 32 in the ZeroLoop!
		
	// Compute the length of the string as the difference between
	// the address of the zero byte and the start of the string.
	
	GotLen:
	sub( zsrc, ecx );
			
	// Make sure our second string pointer is non-null.
	
	mov( ssrc, edi );
	test( edi, edi );
	jz AttemptToDerefNULL;
	
	// Compute the length of the resultant string and allocate new
	// storage for that string:
	
	mov( (type str.strRec [edi]).length, eax );
	add( ecx, eax );
	str.alloc( eax );
	push( eax );		// Save new string pointer.
	push( ecx );		// Save length of zstr
	mov( edi, esi );	// Copy the HLA string first.
	mov( (type str.strRec [edi]).length, ecx );
	mov( eax, edi );
	rep.movsb();
	mov( #0, (type char [edi]) );	// Zero-terminate, in case ecx is zero.
	
	// Copy the zstr second:
	
	pop( ecx );			// Retrieve zstr length
	pop( eax );			// Retrieve new string pointer.
	test( ecx, ecx );
	jz allDone;
	
		mov( zsrc, esi );
		rep.movsb();
		
		// Zero-terminate the string
		
		mov( #0, (type char [edi]) );
		
		// Compute the length of the new string:
		
		sub( eax, edi );
		mov( edi, (type str.strRec [eax]).length );
	
	allDone:			
	popfd();
	pop( esi );
	pop( edi );
	pop( ecx );
	pop( ebp );
	ret( _parms_ );
	
AttemptToDerefNULL:
	raise( ex.AttemptToDerefNULL );

end a_catz;
	
end stringUnit;




	

