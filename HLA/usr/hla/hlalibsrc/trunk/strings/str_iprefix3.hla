
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit stringUnit;
?@nodisplay := true;
?@nostackalign := true;

#include( "strings.hhf" );
#include( "excepts.hhf" );



/*****************************************************************/
/*                                                               */
/* str.iprefix3-                                                 */
/*                                                               */
/* Similar to the str.iprefix2 function, except this one         */
/* allows an index into src1 that specifies where the            */
/* comparison is to begin.                                       */
/*                                                               */
/* Returns true in EAX if src1[offs] begins with the string src2 */
/* (that is, if src2 is a prefix of src1[offs]).                 */
/* This routine does a case insensitive comparison.				 */
/*                                                               */
/*****************************************************************/

procedure str.iprefix3( src1:string; offs:dword; src2:string ); 
	@noframe;
begin iprefix3;

	push( ebp );
	mov( esp, ebp );
	push( eax );
	push( esi );
	push( edi );
	push( ecx );
	
	
		// Get the string pointers and make sure they are non-NULL.
			
		mov( src1, edi );
		mov( src2, esi );
		
		test( esi, esi );
		jz AttemptToDerefNULL;
		
		test( edi, edi );
		jz AttemptToDerefNULL;
		
		// Make sure that offs doesn't exceed src1's length.
		
		mov( (type str.strRec [edi]).length, eax );
		cmp( eax, offs );
		jb StringIndexError;
	
		
		// If src2's length is greater than src1's length
		// (after adjusting for the offset value),
		// then src2 cannot be a prefix of src1.

		mov( (type str.strRec [esi]).length, ecx );
		sub( offs, eax );
		cmp( ecx, eax );
		jbe couldBePrefix;
		
			xor( eax, eax );	// Assume no prefix.
			jmp strcmp;
			
		couldBePrefix:
		
		
		// Okay, length( src2 ) <= length( src1 + offs ), so compare the 
		// strings up to the length of src2 starting at index "offs":
		
		add( offs, edi );
		xor( eax, eax );
		test( ecx, ecx );
		jz strcmp;		// Fail if src2's length is zero.
		
		inc( ecx );
		dec( esi );
		dec( edi );
		striCmpLp:
			inc( esi );
			inc( edi );
			dec( ecx );
			jz cmpDone;

			mov( [esi], al );
			cmp( al, [edi] );
			je striCmpLp;

			// If it didn't match, try flipping the case and
			// trying again:

			cmp( al, 'A' );
			jb cmpDone;
			cmp( al, 'Z' );
			jbe switchCase;
			cmp( al, 'a' );
			jb cmpDone;
			cmp( al, 'z' );
			ja cmpDone;
		  switchCase:
			xor( $20, al );
			cmp( al, [edi] );
			je striCmpLp;

		cmpDone:

			// Set EAX according to the comparison result.
			
			sete( al );
			and( 1, eax );
		
	strcmp:
	pop( ecx );
	pop( edi );
	pop( esi );
	cmp( al, 1 );		// Set zero flag for return if equal.
	cmc();
	pop( eax );
	pop( ebp );
	ret( _parms_ );
		
AttemptToDerefNULL:
	raise( ex.AttemptToDerefNULL );

StringIndexError:
	raise( ex.StringIndexError );

end iprefix3;

end stringUnit;




	

