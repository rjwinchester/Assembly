
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit stringUnit;
?@nodisplay := true;
?@nostackalign := true;

#include( "strings.hhf" );
#include( "excepts.hhf" );


/***********************************************************/
/*                                                         */
/* str.prefix-                                             */
/*                                                         */
/* Returns true in EAX if src1 begins with the string src2 */
/* (that is, if src2 is a prefix of src1).                 */
/*                                                         */
/***********************************************************/


procedure str.iprefix2( src1:string; src2:string ); @noframe;
begin iprefix2;

	push( ebp );
	mov( esp, ebp );
	push( eax );
	push( esi );
	push( edi );
	push( ecx );
	xor( eax, eax );	// Assume it's not a prefix.
	
		// Get the string pointers and make sure they are non-NULL.
			
		mov( src1, edi );
		mov( src2, esi );
		
		test( esi, esi );
		jz AttemptToDerefNULL;
		
		test( edi, edi );
		jz AttemptToDerefNULL;
		
		// If src2's length is greater than src1's length
		// then src2 cannot be a prefix of src1.

		mov( (type str.strRec [esi]).length, ecx );
		test( ecx, ecx );
		jz notAPrefix;		// Fail if src2's length is zero.
		
		cmp( ecx, (type str.strRec [edi]).length );
		ja notAPrefix;
		
		// Okay, length( src2 ) <= length( src1 ), so compare the 
		// strings up to the length of src2:
		
		inc( ecx );
		dec( esi );
		dec( edi );
		striCmpLp:
			inc( esi );
			inc( edi );
			dec( ecx );
			jz cmpDone;

			mov( [esi], al );
			cmp( al, [edi] );
			je striCmpLp;

			// If it didn't match, try flipping the case and
			// trying again:

			cmp( al, 'A' );
			jb cmpDone;
			cmp( al, 'Z' );
			jbe switchCase;
			cmp( al, 'a' );
			jb cmpDone;
			cmp( al, 'z' );
			ja cmpDone;
		  switchCase:
			xor( $20, al );
			cmp( al, [edi] );
			je striCmpLp;

		cmpDone:

		// Set EAX according to the comparison result.
		
		sete( al );
		
	notAPrefix:
	pop( ecx );
	pop( edi );
	pop( esi );
	cmp( al, 1 );	// Set zero flag for return if prefix.
	cmc();
	pop( eax );
	pop( ebp );
	ret( _parms_ );
		
AttemptToDerefNULL:
	raise( ex.AttemptToDerefNULL );

StringIndexError:
	raise( ex.StringIndexError );

end iprefix2;

end stringUnit;




	























			  