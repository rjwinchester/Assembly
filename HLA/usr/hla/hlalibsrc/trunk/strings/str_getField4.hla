unit str_field_unit;

//	Synopsis:
//	A "field" in a string consists of a sequence of characters separated by
//	characters in some "delimiter set". This function uses a 'standard'
//	delimiter set (see str.tokenize) that is used to separate command-line
//	parameters. It returns the nth field of a string (n specified by the
//	field' parameter) as a string 
//	Note that the first field is field #0.


//	Version History:
//
//	Revision:	3
//	Author:		Sevag Krikorian
//	Date:		Oct 2, 2007
//	Note:		changed to library format, code improvement
//
//	Revision:	2
//	Author:		Sevag Krikorian
//	Date:		Sep. 02, 2007
//	Note:		separated engine for code reuse, completed  all functions

//	Revision:	1
//	Author:		Sevag Krikorian
//	Date:		July 30, 2007
//	Note:		1st release
//
	
	#includeonce ("strings.hhf")
	#includeonce ("excepts.hhf")
	#includeonce ("memory.hhf")




	procedure str.getField4( s:string; field:dword; cst:cset; dest:string );
		@noframe; @nodisplay; @noalignstack;
		
		// return n'th 'field' in 'dest'
		// use 'cst' delimiter set
		
		var
			saveEAX	:dword;
			saveEBX	:dword;
			saveECX	:dword;
			saveEDX	:dword;
			saveESI	:dword;
			
		
	begin getField4;
		
		push( ebp );
		mov( esp, ebp );
		sub( _vars_, esp );
		
		mov( eax, saveEAX );
		mov( ebx, saveEBX );
		mov( ecx, saveECX );
		mov( edx, saveEDX );
		mov( esi, saveESI );
		
		str._getFieldIndex( s, field, cst );
		jnc done;	// returns false in carry to indicate nothing done
		
		// ebx = start of field, esi = end of field
		// get strlen
		mov( esi, eax );
		sub( ebx, eax );
		
		// let's see if we have enough room in dest!
		mov( dest, edx );
		mov( (type str.strRec[edx]).maxlen, ecx );
		cmp( eax, ecx );
		ja raiseStringOverflow;
		
		push( eax );	// save strlen
		
		sub( 1, ebx );
		_copy_loop:
			add( 1, ebx );
			cmp( ebx, esi );
			je _endcopy;
			mov( [ebx], cl );
			mov( cl, [edx] );
			add( 1, edx );
			jmp _copy_loop;
			
		_endcopy:
			mov( #0, (type byte [edx]) );
			mov( dest, edx );
			pop( (type str.strRec[edx]).length );
			
			stc();	// set carry to true for successful operation
			
		done:
		
		mov( saveEAX, eax );
		mov( saveEBX, ebx );
		mov( saveECX, ecx );
		mov( saveEDX, edx );
		mov( saveESI, esi );
		
		mov( ebp, esp );
		pop( ebp );
		ret( _parms_ );
		
		raiseStringOverflow:
			raise( ex.StringOverflow ); 
		
	end getField4;



end str_field_unit;
