
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit stringUnit;
?@nodisplay := true;
?@nostackalign := true;

#include( "strings.hhf" );
#include( "excepts.hhf" );


/********************************************************/
/*                                                      */
/* rchpos2-                                             */
/*                                                      */
/* Similar to chpos2 above, except this routine searches*/
/* for the character from the end of the string towards */
/* the beginning of the string.                         */
/*                                                      */
/********************************************************/

procedure str.rchpos2( src1:string; src2:char ); @noframe;
begin rchpos2;

	push( ebp );
	mov( esp, ebp );
	push( ecx );
	push( edi );
	pushfd();
	
	mov( src1, edi );
	test( edi, edi );
	jz AttemptToDerefNULL;
	
	try // Protect the direction flag
	
			// If there are any characters in this string, scan from the
			// end of the string to the beginning of the string looking
			// for the specified character.
			
			mov( (type str.strRec [edi]).length, ecx );
			test( ecx, ecx );
			jz NotFound;
			
				add( ecx, edi );			// Pt at end of string.
				dec( edi );					// Start just before zero byte.
				mov( src2, al );
				std();		// Search backwards!
				repne.scasb();				// Do the search.
				jne NotFound;
				
					// Found the char in the string.

					sub( src1, edi );		// Compute index+1 (edi was dec'd!).
					lea( eax, [edi+1] );	// Return index in eax.
					jmp Found;
				
			NotFound:		
			mov( -1, eax );		// Deprecated
				

		Found:
		
	  anyexception
	  	popfd();		// Restore direction flag on exception
		reraise( eax );
		
	endtry;
	popfd();
	pop( edi );
	pop( ecx );
	pop( ebp );
	cmp( eax, -1 );
	ret( _parms_ );	
	
AttemptToDerefNULL:
	raise( ex.AttemptToDerefNULL );

end rchpos2;

end stringUnit;




	

