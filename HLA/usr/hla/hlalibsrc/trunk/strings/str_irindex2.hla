
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit stringUnit;
?@nodisplay := true;
?@nostackalign := true;

#include( "strings.hhf" );
#include( "excepts.hhf" );


/*******************************************************************************/
/*                                                                             */
/* irindex-                                                                    */
/*                                                                             */
/* Similar to iindex function, except irindex searches the string              */
/* backwards (from right to left) to find the *last* occurrence of             */
/* src2 in src1.                                                               */
/*                                                                             */
/*******************************************************************************/




procedure str.irindex2( src1:string; src2:string ); @noframe;
var
	attempts: 	int32;
	
begin irindex2;

	push( ebp );
	mov( esp, ebp );
	sub( _vars_, esp );
	
	push( ebx );
	push( ecx );
	push( edx );
	push( esi );
	push( edi );
	
	mov( src2, edi );
	mov( src1, esi );
	test( esi, esi );
	jz AttemptToDerefNULL;
	test( edi, edi );
	jz AttemptToDerefNULL;

	mov( (type str.strRec [edi]).length, ebx );
	mov( (type str.strRec [esi]).length, attempts );

	
		// If the length of src2 is zero, return failure.
		
		test( ebx, ebx );
		jz noMatch;
		cmp( attempts, ebx );
		jnae noMatch;

			sub( ebx, attempts );
			add( attempts, esi );	// Start at the end of the string.
			mov( edi, edx );	// Save ptr to src2 so we can reset on failure.
			dec( esi );			// Moved out of the loop below.
			repeatUntilAttemptsLT0:
				
				lea( edi, [edx-1] );	// Need -1 'cause of INC below
				lea( ecx, [ebx+1] );	// Need +1 'cause of DEC below
				push( esi );

				striCmpLp:
					inc( esi );
					inc( edi );
					dec( ecx );
					jz theyMatched;

					mov( [esi], al );
					cmp( al, [edi] );
					je striCmpLp;

					// If it didn't match, try flipping the case and
					// trying again:

					cmp( al, 'A' );
					jb NotFound;
					cmp( al, 'Z' );
					jbe switchCase;
					cmp( al, 'a' );
					jb NotFound;
					cmp( al, 'z' );
					ja NotFound;
				  switchCase:
					xor( $20, al );
					cmp( al, [edi] );
					je striCmpLp;
					jmp NotFound;

				  theyMatched:				
					// We found the string, so compute the
					// index and exit the loop.
						
					pop( eax );			// Retrieve ptr into src1.
					inc( eax );			// Adjust for DEC earlier.				
					sub( src1, eax );	// Compute index.
					jmp FoundMatch;
					
				NotFound:
				
				// Okay, src2 didn't match at the current position,
				// try the next character position in src1.
				
				pop( esi );			// Retrieve ptr into src1.
				dec( esi );			// Bump back by one character position.
				dec( attempts );	// Are we done yet?
				jns repeatUntilAttemptsLT0;
			
		noMatch:
		
		// If we get down here, then src2 is not a substring of src1.
		
		mov( -1, eax );		// Deprecated!
		
	FoundMatch:
	pop( edi );
	pop( esi );
	pop( edx );
	pop( ecx );
	pop( ebx );
	cmp( eax, -1 );		// Set carry on success, clear if no match.
	leave();
	ret( _parms_ );
				
AttemptToDerefNULL:
	raise( ex.AttemptToDerefNULL );

StringIndexError:
	raise( ex.StringIndexError );

end irindex2;

end stringUnit;




	

