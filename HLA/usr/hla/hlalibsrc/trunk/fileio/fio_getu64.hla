
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit FileIOUnit;
		



#include( "../include/fileiounit.hhf" )



/******************************************************/
/*                                                    */
/* fgetu64-                                           */
/* fgetu32-                                           */
/* fgetu16-                                           */
/* fgetu8-                                            */
/*                                                    */
/* These routines are similar to the fgetiXX routines */
/* above except they work on unsigned numbers.        */
/*                                                    */
/******************************************************/

// Modified 2-1-2003 to treat EOF as a delimiter character.

procedure fileio.getu64( Handle:dword ); @noframe; @nodisplay;

var
	Delimiters:	cset;
	
begin getu64;

	push( ebp );
	mov( esp, ebp );
	sub( _vars_, esp );
	
	push( ecx );
	
	conv.getDelimiters( Delimiters );
	xor( eax, eax );
	xor( edx, edx );
	xor( ecx, ecx );
	
	// Skip over any leading delimiters:
	
	repeatUntilNC:
	
		fileio.getc( Handle );
		bt( eax, (type dword Delimiters ));
		jc	repeatUntilNC;
	
	
	// Okay, input the digits and convert to a number:
	
	
	repeatUntilC:
	
		// The character must be a numeric digit.
		
		cmp( al, '0' );
		jb convErr;
		cmp( al, '9' );
		ja convErr;
		
		shl( 1, ecx );	// Multiply edx:ecx by 10.
		rcl( 1, edx );
		jc Overflow;
			
		push( edx );	// Save *2.
		push( ecx );
		shl( 1, ecx );
		rcl( 1, edx );
		jc Overflow;
		
		shl( 1, ecx );	// This is *8.
		rcl( 1, edx );
		jc Overflow;
		
		and( $f, eax );	// Add in current digit.
		add( eax, ecx );
		adc( 0, edx );
		jc Overflow;
		
		pop( eax );		// Add in *2.
		add( eax, ecx );
		pop( eax );
		adc( eax, edx );
		jc Overflow;
		
			
		// Skip underscores in the middle of the number.
		
		repeatUntilALne_:
			
			fileio.eof( Handle );
			cmp( al, false );
			jne exitIfEOF;

			fileio.getc( Handle );
			cmp( al, '_' );
			je repeatUntilALne_;
			
		movzx( al, eax );
		bt( eax, (type dword Delimiters )); 
		jnc repeatUntilC;
		
	exitIfEOF:
		mov( ecx, eax );
		pop( ecx );
		leave();
		ret( _parms_ );
	
convErr:
	raise( ex.ConversionError );

Overflow:
	raise( ex.ValueOutOfRange );
		
end getu64;

	
end FileIOUnit;

