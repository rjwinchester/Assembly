
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit FileIOUnit;
		
#include( "excepts.hhf" )
#include( "../include/fileiounit.hhf" )
#include( "os.hhf" )

/****************************************************************/
/*                                                              */
/* files-                                                       */
/*                                                              */
/* Returns all the files in the current working directory.      */
/*                                                              */
/****************************************************************/



#if( os.win32 )

	iterator filesys.fileInCwd; @nodisplay;
	var
		eaxSave		:dword;
		ecxSave		:dword;
		edxSave		:dword;
		handle		:dword;
		FileData	:win.Win32FindData;
		
	begin fileInCwd;

		mov( edx, edxSave );
		mov( ecx, ecxSave );
		mov( eax, eaxSave );
		
		 
		// Find the first matching file (if one exists).
		// Save the handle for use by FindNextFile.
		
		win.FindFirstFile( "*", FileData );
		mov( eax, handle );
		
		// If we matched at least one filename, return
		// the corresponding string and call the
		// FindNextFile routine to match any additional
		// filenames.
		
		cmp( eax, win.INVALID_HANDLE_VALUE );
		je allDone;
		
				repeatUntilNoMore:

					mov( FileData.FileAttributes, eax );
					and
					(
						win.FILE_ATTRIBUTE_HIDDEN | 
							win.FILE_ATTRIBUTE_SYSTEM | 
								win.FILE_ATTRIBUTE_DIRECTORY,
						eax
					);
					jnz notAFile;
					
						// FindFirstFile & FindNextFile return
						// zero-terminated strings.  Convert these
						// to HLA compatible strings and return
						// the converted string:
						
						lea( eax, FileData.FileName );
						str.a_cpyz( eax );

						// Return the converted string to the FOREACH
						// loop and then free the storage associated
						// with the string.
						
						mov( ecxSave, ecx );
						mov( edxSave, edx );
						yield();		// Return string to FOREACH loop.
						mov( edx, edxSave );
						mov( ecx, ecxSave );
					
					notAFile:
					
						// Get the next filename in the list:
						
						win.FindNextFile( handle, FileData );
						cmp( al, false );
						jne repeatUntilNoMore;

			
			
			// When we've processed all the filenames in the list,
			// call FindClose to free the handle and other resources.
			
			win.FindClose( handle );
			
		allDone:
		mov( edxSave, edx );
		mov( ecxSave, ecx );
		mov( eaxSave, eax );

	end fileInCwd;
	
#elseif( os.linux )

	iterator filesys.fileInCwd; @nodisplay;
	var
		handle		:dword;
		filename	:string;
		eaxSave		:dword;
		ebxSave		:dword;
		ecxSave		:dword;
		edxSave		:dword;
		ediSave		:dword;
		esiSave		:dword;
		s			:linux.stat64_t;
		stringBuf	:char[1024];
		dBuf		:byte[4096];
		
		
	begin fileInCwd;

		mov( esi, esiSave );
		mov( edi, ediSave );
		mov( edx, edxSave );
		mov( ecx, ecxSave );
		mov( ebx, ebxSave );
		mov( eax, eaxSave );
		pushfd();
		
		// Initialize string that will hold the retrieved filename:
		
		str.init( stringBuf, 1024 );
		mov( eax, filename );
		
		linux.open( ".", linux.o_rdonly, linux.s_irwxu );
		mov( eax, handle );
		cmp( eax, 0 );
		jle openFailed;

			getNextEnt:

				linux.getdents
				( 
					handle, 
					(type linux.dirent64 dBuf), 
					@size(dBuf) 
				);
				cmp( eax, 0 );
				jle noEntries;
				mov( eax, ecx );
				lea( ebx, dBuf );
				repeatUntilECX0:

					lea( eax, (type linux.dirent64 [ebx]).d_name );
					lea( edx, s );
					linux.stat( eax, [edx] );
					test( eax, eax );
					js notAFile;
					mov( s.st_mode, eax );
					and( linux.s_ifreg, eax );
					jz notAFile;
					
						lea( eax, (type linux.dirent64 [ebx]).d_name );
						str.a_cpyz( eax );
						push( ecx );
						push( ebx );
						mov( esiSave, esi );
						mov( ediSave, edi );
						mov( edxSave, edx );
						mov( ecxSave, ecx );
						mov( ebxSave, ebx );
						yield();
						mov( esi, esiSave );
						mov( edi, ediSave );
						mov( edx, edxSave );
						mov( ecx, ecxSave );
						mov( ebx, ebxSave );
						pop( ebx );
						pop( ecx );
						 
					notAFile:
					movzx( (type linux.dirent64[ebx]).d_reclen, edx );
					add( edx, ebx );
					sub( edx, ecx );
					cmp( ecx, 0 );
					jg repeatUntilECX0;

			jmp getNextEnt;

		noEntries:
			linux.close(handle);
		openFailed:
		mov( esiSave, esi );
		mov( ediSave, edi );
		mov( edxSave, edx );
		mov( ecxSave, ecx );
		mov( ebxSave, ebx );
		mov( eaxSave, eax );
		popfd();

	end fileInCwd;

#elseif( os.freeBSD )

	iterator filesys.fileInCwd; @nodisplay;
	var
		handle		:dword;
		filename	:string;
		eaxSave		:dword;
		ebxSave		:dword;
		ecxSave		:dword;
		edxSave		:dword;
		ediSave		:dword;
		esiSave		:dword;
		s			:bsd.stat_t;
		stringBuf	:char[1024];
		dBuf		:byte[4096];
		
		
	begin fileInCwd;

		mov( esi, esiSave );
		mov( edi, ediSave );
		mov( edx, edxSave );
		mov( ecx, ecxSave );
		mov( ebx, ebxSave );
		mov( eax, eaxSave );
		pushfd();
		
		// Initialize string that will hold the retrieved filename:
		
		str.init( stringBuf, 1024 );
		mov( eax, filename );
		
		bsd.open( ".", bsd.O_RDONLY, bsd.S_IRWXU );
		mov( eax, handle );
		jc openFailed;

			forEachDirEntry:

				bsd.getdents
				( 
					handle, 
					(type bsd.dirent dBuf), 
					@size(dBuf) 
				);
				jc noEntries;
				cmp( eax, 0 );
				jle noEntries;
				mov( eax, ecx );
				lea( ebx, dBuf );
				repeatUntilECX0:

					lea( eax, (type bsd.dirent [ebx]).d_name );
					lea( edx, s );
					bsd.stat( eax, [edx] );
					jc notAFile;
					movzx( s.st_mode, eax );
					and( bsd.S_IFREG, eax );
					jz notAFile;
					
						lea( eax, (type bsd.dirent [ebx]).d_name );
						str.a_cpyz( eax );
						push( ecx );
						push( ebx );
						mov( esiSave, esi );
						mov( ediSave, edi );
						mov( edxSave, edx );
						mov( ecxSave, ecx );
						mov( ebxSave, ebx );
						yield();
						mov( esi, esiSave );
						mov( edi, ediSave );
						mov( edx, edxSave );
						mov( ecx, ecxSave );
						mov( ebx, ebxSave );
						pop( ebx );
						pop( ecx );
						 
					notAFile:
					movzx( (type bsd.dirent [ebx]).d_reclen, edx );
					add( edx, ebx );
					sub( edx, ecx );
					cmp( ecx, 0 );
					jg repeatUntilECX0;

			jmp forEachDirEntry;

		  noEntries:
		    bsd.close(handle);
		    
		openFailed:
		mov( esiSave, esi );
		mov( ediSave, edi );
		mov( edxSave, edx );
		mov( ecxSave, ecx );
		mov( ebxSave, ebx );
		mov( eaxSave, eax );
		popfd();

	end fileInCwd;

#elseif( os.macOS )

	iterator filesys.fileInCwd; @nodisplay;
	var
		handle		:dword;
		filename	:string;
		eaxSave		:dword;
		ebxSave		:dword;
		ecxSave		:dword;
		edxSave		:dword;
		ediSave		:dword;
		esiSave		:dword;
		s			:mac.stat_t;
		stringBuf	:char[1024];
		dBuf		:byte[4096];
		
		
	begin fileInCwd;

		mov( esi, esiSave );
		mov( edi, ediSave );
		mov( edx, edxSave );
		mov( ecx, ecxSave );
		mov( ebx, ebxSave );
		mov( eax, eaxSave );
		pushfd();
		
		// Initialize string that will hold the retrieved filename:
		
		str.init( stringBuf, 1024 );
		mov( eax, filename );
		
		mac.open( ".", mac.O_RDONLY, mac.S_IRWXU );
		mov( eax, handle );
		jc openFailed;

			forEachDirEntry:

				mac.getdents
				( 
					handle, 
					(type mac.dirent dBuf), 
					@size(dBuf) 
				);
				jc noEntries;
				cmp( eax, 0 );
				jle noEntries;
				mov( eax, ecx );
				lea( ebx, dBuf );
				repeatUntilECX0:

					lea( eax, (type mac.dirent [ebx]).d_name );
					lea( edx, s );
					mac.stat( eax, [edx] );
					jc notAFile;
					movzx( s.st_mode, eax );
					and( mac.S_IFREG, eax );
					jz notAFile;
					
						lea( eax, (type mac.dirent [ebx]).d_name );
						str.a_cpyz( eax );
						push( ecx );
						push( ebx );
						mov( esiSave, esi );
						mov( ediSave, edi );
						mov( edxSave, edx );
						mov( ecxSave, ecx );
						mov( ebxSave, ebx );
						yield();
						mov( esi, esiSave );
						mov( edi, ediSave );
						mov( edx, edxSave );
						mov( ecx, ecxSave );
						mov( ebx, ebxSave );
						pop( ebx );
						pop( ecx );
						 
					notAFile:
					movzx( (type mac.dirent [ebx]).d_reclen, edx );
					add( edx, ebx );
					sub( edx, ecx );
					cmp( ecx, 0 );
					jg repeatUntilECX0;

			jmp forEachDirEntry;

		  noEntries:
		    mac.close(handle);
		    
		openFailed:
		mov( esiSave, esi );
		mov( ediSave, edi );
		mov( edxSave, edx );
		mov( ecxSave, ecx );
		mov( ebxSave, ebx );
		mov( eaxSave, eax );
		popfd();

	end fileInCwd;

#else

	#print( "Unknown OS!" )

#endif

end FileIOUnit;

