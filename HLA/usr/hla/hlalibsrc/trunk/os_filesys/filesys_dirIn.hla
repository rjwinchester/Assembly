
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit FileIOUnit;
		
#include( "excepts.hhf" )
#include( "../include/fileiounit.hhf" )
#include( "os.hhf" )

/****************************************************************/
/*                                                              */
/* dirIn-                                                       */
/*                                                              */
/* Returns all the directories in the specified directory.      */
/*                                                              */
/****************************************************************/



#if( os.win32 )

	iterator filesys.dirIn( directory:string ); @nodisplay; @noframe;
	var
		eaxSave		:dword;
		ecxSave		:dword;
		edxSave		:dword;
		handle		:dword;
		pathName	:string;
		filename	:string;
		FileData	:win.Win32FindData;
		
	begin dirIn;

		push( ebp );
		mov( esp, ebp );
		sub( _vars_, esp );
		
		mov( edx, edxSave );
		mov( ecx, ecxSave );
		mov( eax, eaxSave );
		
		
		str.talloc( 1024 );
		mov( eax, pathName );
		
		str.cpy( directory, pathName );
		
		// Quick check to make sure we don't overflow pathName:
		
		mov( directory, eax );
		mov( (type str.strRec [eax]).length, ecx );
		cmp( ecx, 1000 );
		jae StringOverflow;
		
		// Okay, tack on a "\*" to the path supplied (no '\' if
		// there is already one present):
		
		mov( [eax+ecx-1], dl );
		mov( pathName, eax );
		cmp( dl, '/' );
		je haveSeparator;
		cmp( dl, '\' );
		je haveSeparator;

			// Add "\*" suffix to pathname:
			
			mov( hla.asWord( "\*" ), (type word [eax]) );
			mov( 0, (type byte [eax+2]));
			add( 2, (type str.strRec [eax]).length );
			jmp havePath;
			
		haveSeparator:
		
			// directory already has a "/" or "\" suffix,
			// so just add "*":
			
			mov( hla.asWord( "*" ), (type word [eax]));
			add( 1, (type str.strRec [eax]).length );
			
		havePath:
		 
		// Find the first matching file (if one exists).
		// Save the handle for use by FindNextFile.
		
		win.FindFirstFile( pathName, FileData );
		mov( eax, handle );
		
		// If we matched at least one filename, return
		// the corresponding string and call the
		// FindNextFile routine to match any additional
		// filenames.
		
		cmp( eax, win.INVALID_HANDLE_VALUE );
		je allDone;
		
			repeatUntilNoMore:
			
				mov( FileData.FileAttributes, eax );
				and( win.FILE_ATTRIBUTE_DIRECTORY, eax );
				jz notADir;
			
				
					// FindFirstFile & FindNextFile return
					// zero-terminated strings.  Convert these
					// to HLA compatible strings and return
					// the converted string:
					
					lea( eax, FileData.FileName );
					str.a_cpyz( eax );
					mov( eax, filename );
					cmp( (type byte [eax]), 0 );	// Shouldn't happen, but...
					je notADir2;
					
						// Don't return '.' and '..':
					
						cmp( (type byte [eax]), '.' );
						jne yieldFile;
						cmp( (type byte [eax+1]), 0 );
						je notADir2;
						cmp( (type byte [eax+1]), '.' );
						jne yieldFile;
						cmp( (type byte [eax+2]), 0 );
						je notADir2;
						


						yieldFile:
						
							// Return the converted string to the FOREACH
							// loop and then free the storage associated
							// with the string.
							
							mov( ecxSave, ecx );
							mov( edxSave, edx );
							yield();		// Return string to FOREACH loop.
							mov( edx, edxSave );
							mov( ecx, ecxSave );
							jmp notADir;
							
					notADir2:
						str.free( filename );
				
				notADir:
				
				// Get the next filename in the list:
				
				win.FindNextFile( handle, FileData );
				cmp( al, false );
				jne repeatUntilNoMore;

			
			// When we've processed all the filenames in the list,
			// call FindClose to free the handle and other resources.
			
			win.FindClose( handle );
			
		allDone:
		mov( edxSave, edx );
		mov( ecxSave, ecx );
		mov( eaxSave, eax );
		leave();
		add( 4, esp );		// Remove ptr to foreach body
		ret( _parms_ );
		
	StringOverflow:
		raise( ex.StringOverflow );

	end dirIn;
	
#elseif( os.linux )

	iterator filesys.dirIn( directory:string ); @nodisplay; @noframe;
	var
		filename	:string;
		handle		:dword;
		eaxSave		:dword;
		ebxSave		:dword;
		ecxSave		:dword;
		edxSave		:dword;
		ediSave		:dword;
		esiSave		:dword;
		s			:linux.stat64_t;
		dBuf		:byte[4096];
		
	readonly
		curDir	:string := ".";
			
		
	begin dirIn;

		push( ebp );
		mov( esp, ebp );
		sub( _vars_, esp );
		
		mov( esi, esiSave );
		mov( edi, ediSave );
		mov( edx, edxSave );
		mov( ecx, ecxSave );
		mov( ebx, ebxSave );
		mov( eax, eaxSave );
		pushfd();
		
		mov( directory, eax );
		test( eax, eax );
		jz emptyString;
		cmp( (type str.strRec [eax]).length, 0 );
		jne notEmptyString;
		
		emptyString:
			mov( curDir, eax );
			mov( eax, directory );
			
		notEmptyString:

		linux.open( directory, linux.o_rdonly, linux.s_irwxu );
		mov( eax, handle );
		test( eax, eax );
		jz noDirEntries;

			forAllDirEntries:

				linux.getdents
				( 
					handle, 
					(type linux.dirent64 dBuf), 
					@size(dBuf) 
				);
				cmp( eax, 0 );
				jle noDirEntries;
				mov( eax, ecx );
				lea( ebx, dBuf );
				repeatUntilDirEntDone:

					lea( eax, (type linux.dirent64 [ebx]).d_name );
					lea( edx, s );
					linux.stat( eax, [edx] );
					mov( s.st_mode, eax );
					and( linux.s_ifdir, eax );
					jz notADir;
						
						lea( eax, (type linux.dirent64 [ebx]).d_name );
						str.a_cpyz( eax );
						mov( eax, filename );
						cmp( (type byte [eax]), 0 );	// Shouldn't happen, but...
						je notADir2;
						
							// Don't return '.' and '..':
						
							cmp( (type byte [eax]), '.' );
							jne yieldFile;
							cmp( (type byte [eax+1]), 0 );
							je notADir2;
							cmp( (type byte [eax+1]), '.' );
							jne yieldFile;
							cmp( (type byte [eax+2]), 0 );
							je notADir2;
							


							yieldFile:
								push( ecx );
								push( ebx );
								mov( esiSave, esi );
								mov( ediSave, edi );
								mov( edxSave, edx );
								mov( ecxSave, ecx );
								mov( ebxSave, ebx );
								mov( filename, eax );
								yield();
								mov( esi, esiSave );
								mov( edi, ediSave );
								mov( edx, edxSave );
								mov( ecx, ecxSave );
								mov( ebx, ebxSave );
								pop( ebx );
								pop( ecx );
								jmp notADir;
								
						notADir2:
							str.free( filename );
						
					notADir:
					movzx( (type linux.dirent64[ebx]).d_reclen, edx );
					add( edx, ebx );
					sub( edx, ecx );
					cmp( ecx, 0 );
					jg repeatUntilDirEntDone;
					
			jmp forAllDirEntries;

		noDirEntries:
		linux.close(handle);
		mov( esiSave, esi );
		mov( ediSave, edi );
		mov( edxSave, edx );
		mov( ecxSave, ecx );
		mov( ebxSave, ebx );
		mov( eaxSave, eax );
		popfd();
		leave();
		add( 4, esp );	// Remove ptr to foreach loop body
		ret( _parms_ );

	end dirIn;

#elseif( os.freeBSD )

	iterator filesys.dirIn( directory:string ); @nodisplay; @noframe;
	var
		filename	:string;
		handle		:dword;
		eaxSave		:dword;
		ebxSave		:dword;
		ecxSave		:dword;
		edxSave		:dword;
		ediSave		:dword;
		esiSave		:dword;
		s			:bsd.stat_t;
		stringBuf	:char[1024];
		dBuf		:byte[4096];
		
	readonly
		curDir	:string := ".";
			
		
	begin dirIn;

		push( ebp );
		mov( esp, ebp );
		sub( _vars_, esp );
		
		mov( esi, esiSave );
		mov( edi, ediSave );
		mov( edx, edxSave );
		mov( ecx, ecxSave );
		mov( ebx, ebxSave );
		mov( eax, eaxSave );
		pushfd();
		
		mov( directory, eax );
		test( eax, eax );
		jz emptyString;
		cmp( (type str.strRec [eax]).length, 0 );
		jne notEmptyString;
		
		emptyString:
			mov( curDir, eax );
			mov( eax, directory );
			
		notEmptyString:

		bsd.open( directory, bsd.O_RDONLY, bsd.S_IRWXU );
		jc openFailed;
		
			mov( eax, handle );
			forAllDirEntries:

				bsd.getdents
				( 
					handle, 
					(type bsd.dirent dBuf), 
					@size(dBuf) 
				);
				jc noDirEntries;
				cmp( eax, 0 );
				jle noDirEntries;
				mov( eax, ecx );
				lea( ebx, dBuf );
				repeatUntilDirEntDone:

					lea( eax, (type bsd.dirent [ebx]).d_name );
					lea( edx, s );
					bsd.stat( eax, [edx] );
					jc noDirEntries;
					movzx( s.st_mode, eax );
					and( bsd.S_IFDIR, eax );
					jz notADir;
						
						lea( eax, (type bsd.dirent [ebx]).d_name );
						str.a_cpyz( eax );
						mov( eax, filename );
						cmp( (type byte [eax]), 0 );	// Shouldn't happen, but...
						je notADir2;
						
							// Don't return '.' and '..':
						
							cmp( (type byte [eax]), '.' );
							jne yieldFile;
							cmp( (type byte [eax+1]), 0 );
							je notADir2;
							cmp( (type byte [eax+1]), '.' );
							jne yieldFile;
							cmp( (type byte [eax+2]), 0 );
							je notADir2;
							


							yieldFile:
								push( ecx );
								push( ebx );
								mov( esiSave, esi );
								mov( ediSave, edi );
								mov( edxSave, edx );
								mov( ecxSave, ecx );
								mov( ebxSave, ebx );
								mov( filename, eax );
								yield();
								mov( esi, esiSave );
								mov( edi, ediSave );
								mov( edx, edxSave );
								mov( ecx, ecxSave );
								mov( ebx, ebxSave );
								pop( ebx );
								pop( ecx );
								jmp notADir;
								
						notADir2:
							str.free( filename );
						
					notADir:
					movzx( (type bsd.dirent [ebx]).d_reclen, edx );
					add( edx, ebx );
					sub( edx, ecx );
					cmp( ecx, 0 );
					jg repeatUntilDirEntDone;
					
			jmp forAllDirEntries;

		  noDirEntries:
		    bsd.close(handle);
		
		openFailed:
		mov( esiSave, esi );
		mov( ediSave, edi );
		mov( edxSave, edx );
		mov( ecxSave, ecx );
		mov( ebxSave, ebx );
		mov( eaxSave, eax );
		popfd();
		leave();
		add( 4, esp );	// Remove ptr to foreach loop body
		ret( _parms_ );

	end dirIn;

#elseif( os.macOS )

	iterator filesys.dirIn( directory:string ); @nodisplay; @noframe;
	var
		filename	:string;
		handle		:dword;
		eaxSave		:dword;
		ebxSave		:dword;
		ecxSave		:dword;
		edxSave		:dword;
		ediSave		:dword;
		esiSave		:dword;
		s			:mac.stat_t;
		stringBuf	:char[1024];
		dBuf		:byte[4096];
		
	readonly
		curDir	:string := ".";
			
		
	begin dirIn;

		push( ebp );
		mov( esp, ebp );
		sub( _vars_, esp );
		
		mov( esi, esiSave );
		mov( edi, ediSave );
		mov( edx, edxSave );
		mov( ecx, ecxSave );
		mov( ebx, ebxSave );
		mov( eax, eaxSave );
		pushfd();
		
		mov( directory, eax );
		test( eax, eax );
		jz emptyString;
		cmp( (type str.strRec [eax]).length, 0 );
		jne notEmptyString;
		
		emptyString:
			mov( curDir, eax );
			mov( eax, directory );
			
		notEmptyString:

		mac.open( directory, mac.O_RDONLY, mac.S_IRWXU );
		jc openFailed;
		
			mov( eax, handle );
			forAllDirEntries:

				mac.getdents
				( 
					handle, 
					(type mac.dirent dBuf), 
					@size(dBuf) 
				);
				jc noDirEntries;
				cmp( eax, 0 );
				jle noDirEntries;
				mov( eax, ecx );
				lea( ebx, dBuf );
				repeatUntilDirEntDone:

					lea( eax, (type mac.dirent [ebx]).d_name );
					lea( edx, s );
					mac.stat( eax, [edx] );
					jc noDirEntries;
					movzx( s.st_mode, eax );
					and( mac.S_IFDIR, eax );
					jz notADir;
						
						lea( eax, (type mac.dirent [ebx]).d_name );
						str.a_cpyz( eax );
						mov( eax, filename );
						cmp( (type byte [eax]), 0 );	// Shouldn't happen, but...
						je notADir2;
						
							// Don't return '.' and '..':
						
							cmp( (type byte [eax]), '.' );
							jne yieldFile;
							cmp( (type byte [eax+1]), 0 );
							je notADir2;
							cmp( (type byte [eax+1]), '.' );
							jne yieldFile;
							cmp( (type byte [eax+2]), 0 );
							je notADir2;
							


							yieldFile:
								push( ecx );
								push( ebx );
								mov( esiSave, esi );
								mov( ediSave, edi );
								mov( edxSave, edx );
								mov( ecxSave, ecx );
								mov( ebxSave, ebx );
								mov( filename, eax );
								yield();
								mov( esi, esiSave );
								mov( edi, ediSave );
								mov( edx, edxSave );
								mov( ecx, ecxSave );
								mov( ebx, ebxSave );
								pop( ebx );
								pop( ecx );
								jmp notADir;
								
						notADir2:
							str.free( filename );
						
					notADir:
					movzx( (type mac.dirent [ebx]).d_reclen, edx );
					add( edx, ebx );
					sub( edx, ecx );
					cmp( ecx, 0 );
					jg repeatUntilDirEntDone;
					
			jmp forAllDirEntries;

		  noDirEntries:
		    mac.close(handle);
		
		openFailed:
		mov( esiSave, esi );
		mov( ediSave, edi );
		mov( edxSave, edx );
		mov( ecxSave, ecx );
		mov( ebxSave, ebx );
		mov( eaxSave, eax );
		popfd();
		leave();
		add( 4, esp );	// Remove ptr to foreach loop body
		ret( _parms_ );

	end dirIn;

#else 

	#print( "Unknown OS!" )

#endif

end FileIOUnit;

