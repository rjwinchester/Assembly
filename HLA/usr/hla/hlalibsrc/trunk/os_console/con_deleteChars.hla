
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit consoleUnit;

#include( "console.hhf" )
#includeonce( "os.hhf" )
#includeonce( "stdout.hhf" )

?@nodisplay := true;
?@noalignstack := true;	

#if( os.linux | os.freeBSD | os.macOS )
	
	// deleteChars-
	//
	// Deletes n characters at the current cursor position
	// by shifting the characters after the cursor over n cells
	// to the left.

	procedure console.deleteChars( n:dword ); @nodisplay; @noframe;
	const
		sys_write		:= 4;
		
	var
		output	:char[16];
 
	begin deleteChars;

	
		push( ebp );
		mov( esp, ebp );
		sub( _vars_, esp );
		
		push( eax );
		push( ebx );
		push( ecx );
		push( edx );
		
		mov( #$1b, output[0] );
		mov( '[', output[1] );
		lea( ecx, output[2] );		
		console.utoa( n, ecx );
		mov( 'P', (type char [ecx]));
		inc( ecx );
		
		lea( edx, output );
		xchg( ecx, edx );
		sub( ecx, edx );
		stdout.write( [ecx], edx );
		
		pop( edx );
		pop( ecx );
		pop( ebx );
		pop( eax );
		leave();
		ret( _parms_ );

	end deleteChars;
	
#elseif( os.win32 )

	#include( "stdout.hhf" )
	
	type 
	 	COORD: record
			x: word;
			y: word;
		endrecord;


	 	SMALL_RECT: record
			Left: word;
			Top: word;
			Right: word;
			Bottom: word;
		endrecord;
	
	 	CHARTYPE: union
			UnicodeChar: word;
			AsciiChar: byte;
		endunion;

	 	CHAR_INFO: record
			CharVal: CHARTYPE;
			Attributes: word;
		endrecord;

		CONSOLE_SCREEN_BUFFER_INFO: record

			dwSize: COORD;
			dwCursorPosition: COORD;
			wAttributes: word;
			srWindow: SMALL_RECT;
			dwMaximumWindowSize: COORD;
			
		endrecord;
		
	static
		FillConsoleOutputAttribute: procedure
		( 
				hConsoleOutput			:dword; 
				wAttribute				:word; 
				nLength					:dword; 
				dwWriteCoord			:COORD; 
			var	lpNumberOfAttrsWritten	:dword
		);
			@stdcall;
			@returns( "eax" );
			@external( "__imp__FillConsoleOutputAttribute@20" );

		FillConsoleOutputCharacter: procedure
		( 
			hConsoleOutput:				dword; 
			cCharacter:					char; 
			nLength:					dword; 
			dwWriteCoord:				COORD; 
			var	lpNumberOfAttrsWritten:	dword
		);
			@stdcall;
			@returns( "eax" );
			@external( "__imp__FillConsoleOutputCharacterA@20" );

			
		GetConsoleScreenBufferInfo: procedure
		( 
				handle:	dword; 
			var csbi:	CONSOLE_SCREEN_BUFFER_INFO
		);
			@stdcall;
			@returns( "eax" );
			@external( "__imp__GetConsoleScreenBufferInfo@8" );
		
		ScrollConsoleScreenBuffer: procedure
		( 
				hConsoleOutput:			dword; 
			var	lpScrollRectangle:		SMALL_RECT; 
			var	lpClipRectangle:		SMALL_RECT; 
				dwDestinationOrigin:	COORD; 
			var	lpFill:					CHAR_INFO
		);
			@stdcall;
			@returns( "eax" );
			@external( "__imp__ScrollConsoleScreenBufferA@20" );
	

	
	
	procedure console.deleteChars( n:dword ); @nodisplay; @noframe;
	var
		handle			:dword;
		size			:dword;
		src				:SMALL_RECT;
		dest			:COORD;
		ci				:CHAR_INFO;
		csbi			:CONSOLE_SCREEN_BUFFER_INFO;
		
	begin deleteChars;

		push( ebp );
		mov( esp, ebp );
		sub( _vars_, esp );
		
		pushad();
		
		stdout.handle();
		mov( eax, handle );
		
		GetConsoleScreenBufferInfo( handle, csbi );

		// If n is greater than the number of characters 
		// left on the line, then just do a clrToEOLN.

		mov( csbi.dwCursorPosition.x, ax );
		add( (type word n), ax );
		if( ax >= csbi.dwSize.x ) then

			console.clrToEOLN();

		else

			// Compute the bounds of the rectangle to move.
			// And the destination coordinate to move it to.

			mov( csbi.dwCursorPosition.x, ax );
			mov( ax, dest.x );
			add( (type word n), ax );
			mov( ax, src.Left );

			mov( csbi.dwCursorPosition.y, ax );
			mov( ax, dest.y );
			mov( ax, src.Top );
			mov( ax, src.Bottom );

			mov( csbi.dwSize.x, ax );
			mov( ax, src.Right );

			// Set up the fill char and attribute:

			mov( ' ', ci.CharVal.AsciiChar );
			mov( csbi.wAttributes, ax );
			mov( ax, ci.Attributes );

			// Okay, scroll the screen up one line:

			ScrollConsoleScreenBuffer
			(
				handle,
				src,
				NULL,	// NULL clipping region.
				#{
					pushd( (type dword dest));
				}#,
				ci
			);

		endif;
		popad();	
		leave();
		ret( _parms_ );
		
	end deleteChars;	

#endif
	
end consoleUnit;
