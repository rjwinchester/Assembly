
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit consoleUnit;

#include( "console.hhf" )
#includeonce( "os.hhf" )
#includeonce( "stdout.hhf" )


?@nodisplay := true;
?@noalignstack := true;	

#if( os.linux | os.freeBSD | os.macOS )
	
	
	// clrToBOLN - Clears from the cursor to the beginning of the line
	//
	// Linux:
	// ANSI ESC sequence is ESC "[1K"

	procedure console.clrToBOLN; @noframe;
	const
		sys_write		:= 4;

	readonly
		clrBOLNStr:byte; @nostorage;
				byte #$1b "[1K";
				
	begin clrToBOLN;
	
		push( eax );
		push( ebx );
		push( ecx );
		push( edx );

		stdout.write( &clrBOLNStr, 4 );
		
		pop( edx );
		pop( ecx );
		pop( ebx );
		pop( eax );
		ret();
		
	end clrToBOLN;

#elseif( os.win32 )

	#include( "stdout.hhf" )
	
	type 
	 	COORD: record
			x: word;
			y: word;
		endrecord;


	 	SMALL_RECT: record
			Left: word;
			Top: word;
			Right: word;
			Bottom: word;
		endrecord;
	
		CONSOLE_SCREEN_BUFFER_INFO: record

			dwSize: COORD;
			dwCursorPosition: COORD;
			wAttributes: word;
			srWindow: SMALL_RECT;
			dwMaximumWindowSize: COORD;
			
		endrecord;
		
	static
		FillConsoleOutputAttribute: procedure
		( 
				hConsoleOutput			:dword; 
				wAttribute				:word; 
				nLength					:dword; 
				dwWriteCoord			:COORD; 
			var	lpNumberOfAttrsWritten	:dword
		);
			@stdcall;
			@returns( "eax" );
			@external( "__imp__FillConsoleOutputAttribute@20" );

		FillConsoleOutputCharacter: procedure
		( 
			hConsoleOutput:				dword; 
			cCharacter:					char; 
			nLength:					dword; 
			dwWriteCoord:				COORD; 
			var	lpNumberOfAttrsWritten:	dword
		);
			@stdcall;
			@returns( "eax" );
			@external( "__imp__FillConsoleOutputCharacterA@20" );

			
		GetConsoleScreenBufferInfo: procedure
		( 
				handle:	dword; 
			var csbi:	CONSOLE_SCREEN_BUFFER_INFO
		);
			@stdcall;
			@returns( "eax" );
			@external( "__imp__GetConsoleScreenBufferInfo@8" );
		
	

	
	
	procedure console.clrToBOLN; @noframe;
	var
		handle			:dword;
		size			:dword;
		charsWritten	:dword;
		csbi			:CONSOLE_SCREEN_BUFFER_INFO;
		
	begin clrToBOLN;

		push( ebp );
		mov( esp, ebp );
		sub( _vars_, esp );
		
		pushad();
		
		stdout.handle();
		mov( eax, handle );
		
		// Compute the number of character positions from the beginning
		// of the line to the cursor position.
		
		GetConsoleScreenBufferInfo( handle, csbi );
		movzx( csbi.dwCursorPosition.x, eax );
		mov( eax, size );
		
		// Generate cursor position (0, y):
		
		movzx( csbi.dwCursorPosition.y, ebx );
		shl( 16, ebx );
		
		// Fill the beginning with blanks:
		
		FillConsoleOutputCharacter
		(
			handle, 
			' ', 
			size, 
			(type COORD ebx), 
			charsWritten
		);
		
		// Set the screen attributes:
		
		movzx( csbi.wAttributes, eax );
		FillConsoleOutputAttribute
		(
			handle,
			ax,
			size,
			(type COORD ebx), 
			charsWritten
		);
		
		popad();
		leave();
		ret();
			
	end clrToBOLN;
	

#endif
	
	
	
end consoleUnit;
