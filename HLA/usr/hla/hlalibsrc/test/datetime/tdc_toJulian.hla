program Julian_test;
#include( "stdlib.hhf" )
#include( "dtClass.hhf" )

static
	baseDate	:date.daterec;
	endDate		:date.daterec;
	
	julianDay	:dword;
	
	dc			:dateClass;
	vdc			:virtualDateClass;	
	
begin Julian_test;
	 
	date.setFormat( date.mmddyyyy );
	dc.create();
	vdc.create();

	// This code depends on the fact that date.minYear is 1600!!!
	
	date.pack( 1, 1, date.minYear, baseDate );
	date.pack( 12, 31, date.minYear+2, endDate );
	
	// Note: 2305448 is the Julian Day number for 1/1/1600
	// (determined externally of the stdlib routines).
	
	mov( 2305448, julianDay );
	
	
	while( mov( baseDate, eax ) <= (type dword endDate) ) do
	
		dc.toJulian();
		mov( eax, ecx );
		vdc.toJulian();
		mov( eax, edx );
		date.toJulian( baseDate );
		if
		( 
				eax <> julianDay
			||	eax <> ecx
			||	eax <> edx 
		) then

			stderr.put
			( 
				nl nl 
				"***************************************************" nl
				"dateClass.toJulian failed!", nl,
				"Date= '"
			);
			mov( eax, ebx );
			date.a_toString( baseDate );
			stderr.put
			(
				(type string eax), nl
				"dc = '", dc, "'" nl,
				"vdc= '", vdc, "'" nl,
				"JulianDay=", (type uns32 julianDay), nl
				"date.toJulian(date)=", (type uns32 ebx), nl
				"***************************************************" nl
			);
			stdout.put( "Press Enter to Continue: " );
			stdin.readLn();
			os.exitProcess(1);

		endif;
		inc( julianDay );
		movzx( baseDate.day, eax );
		movzx( baseDate.month, ebx );
		if( ebx <> 2 ) then
		
			if( eax >= date.DaysInMonth[ebx*4] ) then
			
				mov( 1, baseDate.day );
				add( 1, ebx );
				if( ebx > 12 ) then
				
					add( 1, baseDate.year );
					mov( 1, baseDate.month );
					
				else
				
					mov( bl, baseDate.month );
					
				endif;
				
			else
			
				add( 1, baseDate.day );
				
			endif;
			
		else
		
			if( eax < 28 ) then
			
				inc( baseDate.day );
				
			elseif( eax = 28 && date.isLeapYear( baseDate.year )) then
			
				inc( baseDate.day );
				
			else
			
				mov( 1, baseDate.day );
				mov( 3, baseDate.month );
				
			endif;
			
		endif;
		mov( baseDate, eax );
		mov( eax, dc.theDate );
		mov( eax, vdc.theDate );
		
	endwhile;
	stderr.put( "dateClass.toJulian succeeded!" nl );
	os.exitProcess(0);
		
end Julian_test;

													