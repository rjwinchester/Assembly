program addMins_test;
#include( "stdlib.hhf" )

static
	baseTime	:time.timerec;
	sumTime		:time.timerec;
	baseSeconds	:dword;
	seconds		:dword;
	days		:dword;
	_ebx		:uns32;	
	
begin addMins_test;
	
	mov( 0, baseTime );
	mov( 0, baseSeconds );
	repeat
			
		for( mov( 0, ebx ); ebx <= 2800; inc( ebx )) do
		
			intmul( 60, ebx, ecx );
			mov( ecx, _ebx );
			
			mov( baseTime, eax );
			mov( eax, sumTime );			
			try
			
				intmul( 60, ebx, eax );
				add( baseSeconds, eax );
				mov( eax, seconds );
				
				// addMins returns day overflow in EAX:
				
				time.addMins( ebx, sumTime );
				
				// Compute the number of expected day overflows
				// in ECX (0..2):
				
				movzx( baseTime.hours, ecx );	// Convert base time to minutes
				intmul( 60, ecx );
				movzx( baseTime.mins, esi );
				add( esi, ecx );
				
				add( ebx, ecx );				// Add in current minutes
				if( ecx < 24*60 ) then			// and calculate day overflow.
				
					mov( 0, ecx );
					
				elseif( ecx < 48*60 ) then
				
					mov( 1, ecx );
					
				elseif( ecx < 72*60 ) then
				
					mov( 2, ecx );
					
				else
				
					lea( ecx, [eax+1] ); // Guarantee mismatch
				
				endif;
				
				// Error if the number of overflow days
				// does not match.
				
				cmp( eax, ecx );
				jne timeAddMinsFailed1;
				
				// Convert to seconds and verify
				// against our incrementing seconds value:
				
				time.toSecs( sumTime );
				intmul( 60*60*24, ecx );	// Seconds in overflow days.
				add( ecx, eax );			// Seconds plus overflow days.
				cmp( eax, seconds );
				jne timeAddMinsFailed2;
				
			  anyexception
			  
			  	jmp timeAddMinsFailed3;
			  					
			endtry;
				
		
		endfor;
		time.addSecs( 60, baseTime );	// returns 1 on overflow.
		add( 60, baseSeconds );
		
	until( eax <> 0 );
	stderr.put( "time.addMins succeeded!" nl );
	os.exitProcess(0);
	
timeAddMinsFailed3:
	mov( 3, bl );
	jmp timeAddMinsFailed;
	
timeAddMinsFailed2:
	mov( 2, bl );
	jmp timeAddMinsFailed;
	
timeAddMinsFailed1:
	mov( 1, bl );
timeAddMinsFailed:
	stderr.put
	( 
		nl nl 
		"***************************************************" 
		nl
		"time.addMins failed! (", (type uns8 bl), ")", nl,
		"Time= ",
		(type uns8 baseTime.hours),
		":",
		(type uns8 baseTime.mins),
		":",
		(type uns16 baseTime.secs ),
		"+",
		_ebx,
		" Seconds = ", (type uns32 eax), " should be ", (type uns32 seconds),
		nl
	);
	stdout.put( "Press Enter to Continue: " );
	stdin.readLn();
	os.exitProcess(1);
							
end addMins_test;

													