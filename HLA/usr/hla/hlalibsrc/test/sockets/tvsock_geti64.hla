program sgeti64_test; 
#include( "stdlib.hhf" )
#includeOnce( "sockets.hhf" )

readonly
	max64 :int64 := 9223372036854775807;
	min64 :int64 := -9223372036854775808;
	
static
	quit		:dword := 0;
	hostIPadrs	:dword;
	s			:vServer_t;
	timeout		:thunk;
	calls		:dword := 0;
	input64		:int64;
	
	ipAdrsStr	:str.strvar(256);
	cmdLine		:str.strvar(256);
	exmsg		:str.strvar(256);
	
	
	
	procedure cmp64( right:int64 );
	begin cmp64;
	
		cmp( edx, (type dword right[4]));
		jne notEqual;
		cmp( eax, (type dword right));
	  notEqual:
		
	end cmp64;



	procedure connected;
		@nodisplay;
		@nostackalign;
		@noframe;
	var
		handle		:dword;
		bufIndex	:dword;
		object		:pointer to vServer_t;
		ipAdrs		:hla.sockaddr;
		
		buffer		:char[64];
		
	begin connected;

		push( ebp );
		mov( esp, ebp );
		sub( _vars_, esp );
		
		pushad();
		pushfd();
		
		mov( eax, handle );
		mov( esi, object );
		
		/*****************************************************************/
		
		// Testing socket.geti64
		
		try
		
			object.geti64();
			cmp64( 0 );
			if( @ne ) then
			
				raise($1011);
			
			endif;	
			
			object.geti64();
			cmp64( 1 );
			if( @ne ) then
			
				raise($1012);
			
			endif;	
			
			object.geti64();
			cmp64( -1 );
			if( @ne ) then
			
				raise($1013);
			
			endif;	

			object.geti64();
			cmp64( max64 );
			if( @ne ) then
			
				raise($1014);
			
			endif;


			object.geti64();
			cmp64( min64 );
			if( @ne ) then
			
				raise($1015);
			
			endif;


			try
			
				object.geti64();

			  unprotected
			  
			  	// We we got to this point, we failed to raise a conversion error.
				
			  	raise($1016 );
				
			  exception( ex.ConversionError )
			  
			  	// Okay, we succeeded if we got this exception.
				
			  anyexception
			  
			  	// Anything else? Pass it on.
				
			  	raise($1017 );
				
			endtry;
			object.readLn();	// Clean up after ourselves.
			
			
			try
			
				object.geti64();

			  unprotected
			  
			  	// We we got to this point, we failed to raise an overflow error.
				
			  	raise($1018 );
				
			  exception( ex.ValueOutOfRange )
			  
			  	// Okay, we succeeded if we got this exception.
				
			  anyexception
			  
			  	// Anything else? Pass it on.
				
			  	raise($1019 );
				
			endtry;
			object.readLn();
			stderr.put( "socket.geti64 succeeded!" nl );
			
		  anyexception
		  
			stderr.put
			( 
				nl nl 
				"***************************************************" nl
				"socket.geti64 failed! eax=", eax, nl
				"Data read did not match data expected in file" nl
				"***************************************************" nl
				nl 
			);
			stdout.put( "Press Enter to Continue: " );
			stdin.readLn();
			os.exitProcess(1);
			
		endtry;
			
		/*****************************************************************/
		
		object.close();
		mov( 1, quit );
		popfd();
		popad();
		leave();
		ret( _parms_ );
		
	end connected;
	
begin sgeti64_test; 

	thunk timeout :=
		#{
			// On entry to thunk, EAX contains the address of the timeout
			// variable. Set this as desired for the timeout (2 seconds,
			// in this case).
			
			mov( 2, (type hla.timeval [eax]).tv_sec );
			stderr.put( "server", nl );
			
			// On the second call to this thunk, start the client application:
			
			cmp( calls, 1 );
			jne dontRunApp;
			
				#if( os.win32 )
				
					str.put
					(
						cmdLine,
						"cmd /C rclient socket_geti64.data ",
						ipAdrsStr,
						" 9804"
					);
					stdout.put( "cmd=", cmdLine, "'" nl );
					
				#else
				
					str.put
					(
						cmdLine,
						"rclient.exe socket_geti64.data ",
						ipAdrsStr,
						" 9804"
					);
					stdout.put( "cmd=", cmdLine, "'" nl );
					
				#endif
				os.bkgnd( cmdLine );
			
			dontRunApp:
			inc( calls );
			mov( quit, eax );
				
		}#;
	
	try
	
		s.create();
		
		sock.hostAdrs(); 
		mov( eax, hostIPadrs );
		sock.ipToStr( eax, ipAdrsStr );
		
		s.start
		( 
			hostIPadrs, 
			$9804, 
			timeout,
			&connected,
			0
		);
		
	  anyexception
	  
	  	ex.exceptionMsg( eax, exmsg );
	  	stdout.put( "Server Exception: ", exmsg, nl );
		fileio.close( s.handle );
		s.destroy();
		
	endtry;
	 
end sgeti64_test; 
